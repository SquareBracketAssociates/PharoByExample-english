% $Author: ducasse $
% $Date: 2009-08-24 10:17:33 +0200 (Mon, 24 Aug 2009) $
% $Revision: 28563 $

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
     \documentclass[a4paper,10pt,twoside]{book}
     \usepackage[
          papersize={6.13in,9.21in},
          hmargin={.75in,.75in},
          vmargin={.75in,1in},
          ignoreheadfoot
     ]{geometry}
     \input{../common.tex}
     \setboolean{lulu}{true}
% --------------------------------------------
% A4:
%     \documentclass[a4paper,11pt,twoside]{book}
%     \input{../common.tex}
%     \usepackage{a4wide}
% --------------------------------------------
     \graphicspath{{figures/} {../figures/}}
     \begin{document}
\fi
%=================================================================
%\renewmessage{\nnbb}[2]{} % Disable editorial comments
\sloppy

%=================================================================
%\renewcommand{\nnbb}[2]{#2} % Disable editorial comments


\chapter{CRUD in Pharo}
\chapterauthor{\authorsven{}}

CRUD refers to the Create, read, update and delete operations in persistent storage. The term CRUD applications often refers to simple applications managing simple data and that have to be created fast. Often CRUD development is done with languages such as PHP. In this Chapter we want to show that Smalltalk is a really good alternative and that in addition, your CRUD applications can evolve (which is often the problems of the use of traditional languages). We will show you that it  is perfectly possible to write nice web applications in Pharo and really fast and 
Reddit.st adds persistency in a relational database, unit tests as well as web application components to the mix.

We will show how to implement a small but non-trivial web application in Smalltalk using Seaside, Glorp and PostgreSQL. Reddit, is web application where users can post interesting links that get voted up or down. The idea is that the 'best' links end up with the most points automatically. Many other websites exist in the area of social bookmarking, like Delicious, Digg and Hacker News.



The 10 main sections of this article follow the development of the 10 classes making up the application. The focus of the Smalltalk version is not so much on the small size or the high developer productivity, but more on the fact that we can cover so much ground using such powerful frameworks, as well as the natural development flow from model over tests and persistence to web GUI.

The material shown in this Chapter was originally written by Sven Van Caekenberghe and we thanks him for his permission to use it to create this book chapter.  We assume that you understand what web applications are and how Seaside basically works. If not, you should the Seaside Chapter for an introduction \sd{should be in the other book I guess or in omnibus}. We also will assume that you have a basic understanding of relational databases and/or SQL.


\section{First: a Model}

The central object of our application will be \ct{RedditLink}, an interesting URL with a title, a created timestamp and a number of points. It has the following properties: \ct{id url title created points}.

These are naturally instance variables of our class. Create a new class inheriting from \ct{Object} subclass by editing the class template.

\begin{code}{}
Object subclass: #RedditLink 
   instanceVariableNames: !\textbf{'id url title created points'}! 
   classVariableNames: '' 
   poolDictionaries: '' 
   category: 'Reddit'
\end{code}

Next,  use the class refactoring tool to automatically generate accessors (getters and setters) for all our instance variables. With these implemented we can write our \ct{initialize} and \ct{printOn:} methods.

\begin{code}{}
RedditLink>>initialize
    self initialize.
    self points: 0.
    self created: TimeStamp now

RedditLink>>printOn:
    super printOn: stream. 
    stream nextPut: $(. 
    self url printOn: stream. 
    stream nextPut: $,.
    self title printOn: stream. 
    stream nextPut: $)
\end{code}

We also add a method named \ct{posted}  that will return the \ct{Duration} of time the link now exists. 
We will need that when rendering links later on. 
\begin{code}{}
RedditLink>>posted
    ^ TimeStamp now - self created
\end{code}

Apart from creating and displaying RedditLinks, users should be able to vote them up and down. Therefore, we add two action methods, \ct{voteUp} and \ct{voteDown}.

\begin{code}{}
RedditLink>>voteUp
    self points: self points + 1

RedditLink>>voteDown
    self points > 0 ifTrue: [ self points: self points - 1 ]
\end{code}


We also introduce the class method \ct{#withUrl:title:} to create new instances as follows

\begin{code}{}
RedditLink class>>withUrl: url title: title
   ^ self new url: url; title: title; yourself
\end{code}

The core of the RedditLink object is now finished. Everything is ready to make instances and use them.


\section{RedditLinkTests}
\sd{may be we should write it tests first: we will see}

Units tests are very important, not so much in small examples like this one, but especially in larger applications. Having a good set of unit tests with descent coverage helps protect the code during changes. At the same time, unit tests function as working documentation. Instead of writing scratch test code in some workspace, you can just as well write a unit test. We created the class \ct{RedditLinkTests} as a subclass of \ct{TestCase} and add 3 test methods.



After creating a RedditLink object, and/or manipulating it, these methods assert that certain conditions hold. To improve code sharing (we'll need it again in Section 5) we add a method called \ct{assertContractUsing:} to RedditLink that checks the basic contract of the receiver, using the method \ct{assert:} on an arbitrary object. For completeness, we also implement a general \ct{isValid} testing method.


\begin{code}{}
RedditLink>>assertContractUsing: object
   object perform: #assert: with: (self url isNil or: [ self url isKindOf: String ]).
   object perform: #assert: with: (self title isNil or: [ self title isKindOf: String ]).
   object perform: #assert: with: (self created isKindOf: TimeStamp).
   object perform: #assert: with: (self points isKindOf: Integer).
   object perform: #assert: with: (self posted asSeconds >= 0).
   object perform: #assert: with: (self printString isKindOf: String)
   
RedditLink>>isValid
    self assertContractUsing: self
\end{code}

Now we can take advantage of these methods in our tests.

\begin{code}{}
RedditLinkTests>>testInitialState
    | link | 
    link := RedditLink new. 
    link assertContractUsing: self. 
    self assert: link points isZero
\end{code}

\begin{code}{}
RedditLinkTests>>testCreate
    | link url title | 
    url := 'http://www.seaside.st'. 
    title := 'Seaside'. 
    link := RedditLink withUrl: url title: title. 
    link assertContractUsing: self. 
    self assert: link points isZero. 
    self assert: link url = url. 
    self assert: link title = title
\end{code}

\begin{code}{}
RedditLinkTests>>testVoting
    | link | 
    link := RedditLink new. 
    link assertContractUsing: self. 
    self assert: link points isZero. link voteUp. 
    self assert: link points = 1. 
    link voteDown. self assert: 
    link points isZero. 
    link voteDown. 
    self assert: link points isZero
\end{code}


\section{Describing Database Data}

To make our application less trivial, we are going to make our collection of links persistent in a relation database. For this we are going to use Glorp, an object-relational mapping tool. Glorp will take care of all the SQL! To do its magic, Glorp needs a DescriptorSystem that tells it 3 things: the class models involved, the tables involved and the way the two map (which it calls a descriptor). In this simple case we are just mapping one object into one table, so the descriptor system might look a bit verbose. Just remember that Glorp can do much, much more advanced things. Furthermore, there exist extensions to Glorp that implement ActiveRecord style automatic descriptors.

\paragraph{Loading Glorp.}
Here is how you should load Glorp \sd{to be done}


First let us define a new class description named \ct{RedditSchemaDescriptor} subclass of  \ct{DescriptorSystem} for Glorp.

\begin{code}{}
DescriptorSystem subclass: #RedditSchemaDescriptor
   instanceVariableNames: '' 
   classVariableNames: '' 
   poolDictionaries: '' 
   category: 'Reddit-DB'
\end{code}


Using this class we can tell Glorp about our class model, \ct{RedditLink}, and which instance variables are to be persistent attributes. Instead of some XML description, a much more powerful Smalltalk object model is being built. Conventions in methods names are being used to glue things together.


\begin{code}{}
RedditSchemaDescriptor>>constructAllClasses
    ^ super constructAllClasses add: RedditLink; yourself

RedditSchemaDescriptor>>classModelForRedditLink: aClassModel
    #(id url title created points) do: [ :each | aClassModel newAttributeNamed: each ]
\end{code}

The second step is to describe which tables are involved. So here we list all the fields (columns) of our table \ct{REDDIT\_LINKS}. Glorp shields us from the differences between different SQL dialects. Note how id is designated to be a primary key. The serial type will result in an SQL sequence being used. \sd{what is that a serial type}

\begin{code}{}
RedditSchemaDescriptor>>allTableNames
     ^ #( 'REDDIT_LINKS' )

RedditSchemaDescriptor>>tableForREDDIT_LINKS: aTable
     (aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey. 
     aTable createFieldNamed: 'url' type: (platform varchar: 64). 
     aTable createFieldNamed: 'title' type: (platform varchar: 64). 
     aTable createFieldNamed: 'created' type: platform timestamp.
     aTable createFieldNamed: 'points' type: platform integer
\end{code}

The third and final step is the actual descriptor describing the mapping between the class model \ct{RedditLink} and the table \ct{REDDIT\_LINKS}. In this simple case, direct mappings are used between attributes and fields. As we will see in the next sections, Glorp is now ready to do its work

\begin{code}{}
RedditSchemaDescriptor>>descriptorForRedditLink: aDescriptor

     | table | 
     table := self tableNamed: 'REDDIT_LINKS'. 
     aDescriptor table: table. (aDescriptor newMapping: DirectMapping) from: #id to: (table fieldNamed: 'id'). 
     (aDescriptor newMapping: DirectMapping) from: #url to: (table fieldNamed: 'url'). 
     (aDescriptor newMapping: DirectMapping) from: #title to: (table fieldNamed: 'title'). 
     (aDescriptor newMapping: DirectMapping) from: #created to: (table fieldNamed: 'created'). 
     (aDescriptor newMapping: DirectMapping) from: #points to: (table fieldNamed: 'points')
\end{code}

\section{Connecting to the Database}

Let's assume you installed and configured PostgreSQL on some machine and that you created some database there. We now have to specify how Glorp has to connect to PostgreSQL. We do this creating a new class named \ct{RedditDatabaseResource}

\begin{code}{}
Object subclass: #RedditDatabaseResource
   instanceVariableNames: '' 
   classVariableNames: 'DefaultLogin' 
   poolDictionaries: '' 
   category: 'Reddit-DB'
\end{code}

We will add some class methods (as well as a class variable called \ct{DefaultLogin}).

\begin{code}{}
RedditDatabaseResource class>>login
     DefaultLogin ifNil: [ DefaultLogin := self createLogin ]. 
     ^ DefaultLogin

RedditDatabaseResource class>>login: aLogin
     "see #createLogin for an example of how to create a Login object"      
     DefaultLogin := aLogin

RedditDatabaseResource class>>createLogin
     ^ Login new 
          database: PostgreSQLPlatform new; 
          username: 'svc'; 
          password: 'secret'; 
          connectString: 'localhost:5432_playground'; 
          yourself
\end{code}


\sd{this is fuzzy}
The username and password speak for themselves, the \ct{connectString} contains the hostname, port number and database name (after an underscore). Glorp accesses a database through sessions. A session is most easily started from a descriptor system given a login as argument, that's what we do in the \ct{session} helper class method.


\begin{code}{}
RedditDatabaseResource>>session
     ^ RedditSchema sessionForLogin: self login

RedditDatabaseResource>>createTables
     "self createTables" 
     "This has to be done only once, be sure to set #login"
     |session | 
     session := self session. 
     session accessor login; logging: true. 
     session inTransactionDo: [ session createTables ]. 
     session accessor logout
\end{code}

Glorp can even help us to create our \ct{REDDIT\_LINKS} table, that is what the \ct{createTables} class method does. So we truly don't have to use any SQL! The flow should be familiar: get a session, login, do some work in a transaction and logout. By setting logging to true, the generated SQL statements will be printed on the Transcript (comment this out for production use).


\section{Time to test: RedditDatabaseTest}\label{sec:databasetest}

With our RedditSchema descriptor system and our \ct{RedditLinksDatabaseResource} we are now ready to test the persistency of our model. We create another class named \ct{RedditDatabaseTest} which inherits from \ct{TestCase}. These tests need an instance variable called \ct{session} to hold the Glorp session, as well as \ct{setUp} and \ct{teardown} methods.


\begin{code}{}
Object subclass: #RedditDatabaseTest
   instanceVariableNames: 'session' 
   classVariableNames: '' 
   poolDictionaries: '' 
   category: 'Reddit-DB'
\end{code}

\begin{code}{}
RedditDatabaseTest>>setUp
     session := RedditDatabaseResource session.
     session accessor logging: true; login
\end{code}

\begin{code}{}
RedditDatabaseTest>>tearDown
     session accessor logout
\end{code}


Our first test reads all RedditLinks from the database, making sure they are valid and of the expected type. Querying doesn't have to be done in a unit of work or transaction.

\begin{code}{}
RedditDatabaseTest>>testQuery
    |links|
    links := session readManyOf: RedditLink.
     links do: [ :each |
                 each assertContractUsing: self. 
                 self assert: (each isKindOf: RedditLink) ]
\end{code}


The second test creates a new RedditLink and then registers it with the session inside a unit of work. This will effectively save the object in the database. The id of the RedditLink will have a value afterwards. Next we reset the session and query the RedditLink with the known id. After making sure that what we put in got out of the database we delete the object.

\begin{code}{A first test}
RedditDatabaseTest>>testUpdate
    | link url title id |
    url := 'http://www.seaside.st'. 
    title := 'Seaside Unit Test'. 
    link := RedditLink withUrl: url title: title.
    session inUnitOfWorkDo: [ session register: link ]. 
         id := link id. 
     session inUnitOfWorkDo: [ session register: link. link voteUp ].      
     session reset.
\end{code}

 The third test checks if updating an existing persistent object works as expected. Note that the actual modification, the \ct{voteUp}, has to be done inside a unit of work to a registered object for it to be picked up by Glorp.

\begin{code}{}
RedditDatabaseTest>>testCreate
     | link url title id |
     url := 'http://www.seaside.st'.
     title := 'Seaside Unit Test'.
     link := RedditLink withUrl: url title: title.
     session inUnitOfWorkDo: [ session register: link ].
     id := link id.
     self assert: id notNil. 
     session reset.
     link := session readOneOf: RedditLink where: [ :each | each id = id ]. 
     link assertContractUsing: self.
     self assert: link url = url.
     self assert: link title = title. 
     session delete: link
\end{code}


\section{RedditSession}
We are almost ready to start writing the GUI of our actual web application. Seaside web applications often have a session object that keeps the application's state during the user's interaction with it. We need to extend that session with a database session. We define a new class  RedditSession as a subclass of \ct{WASession}. In addition, it has an instance variable called \ct{glorpSession} to hold a Glorp session to the database.

\begin{code}{}
WASession subclass: #RedditSession
   instanceVariableNames: 'glorpSession' 
   classVariableNames: '' 
   poolDictionaries: '' 
   category: 'Reddit-DB'
\end{code}

Note how we are using lazy initialization in the \ct{glorpSession} accessor. In \ct{newGlorpSession} we're making use of our \ct{RedditDatabaseResource}. 

\begin{code}{}
RedditSession>>glorpSession
   glorpSession ifNil: [ glorpSession := self newGlorpSession ]. 
   glorpSession accessor isLoggedIn ifFalse: [ glorpSession accessor login ]. 
   ^ glorpSession
\end{code}

\begin{code}{}
RedditSession>>newGlorpSession
     | session | 
     session := RedditDatabaseResource session. "session accessor logging: true." 
     ^ session
\end{code}

The \ct{unregistered} is a hook called by Seaside whenever a session expires, we use it clean up our Glorp session by doing a log out.

\begin{code}{}
RedditSession>>unregistered
     super unregistered. 
     self teardownGlorpSession
\end{code}

\begin{code}{}
RedditSession>>teardownGlorpSession
     self glorpSession logout
\end{code}

\section{Web Part: the Component \ct{WAReddit}}

We can finally start with our web app itself. Figure 1 shows the main page of the Reddit.st app. There are four sections in this page: a header or title section, some action links, a list of some of the highest or top ranking links and a list if some of the latest or most recent links.

\begin{figure}\begin{center}
\includegraphics[width=8cm]{redditOne}
\caption{}
\end{center}
\end{figure}

We create a \ct{WAComponent} subclass called \ct{WAReddit}. This will become our central or root web app component. We start by writing the rendering methods.

\begin{code}{}
WAReddit>>renderContentOn: html    html heading: 'Reddit.st'. 
    html heading level: 3; with: 'In 10 elegant Smalltalk classes'. 
    self renderActionsOn: html. 
    self renderHighestRankingLinksOn: html. 
    self renderLatestLinksOn: html
\end{code}

\begin{code}{}
WAReddit>>renderActionsOn: html    html paragraph: [ html anchor callback: [ ]; with: 'Refresh'. 
    html anchor callback: [ self inform: 'Not yet implemented' ]; with: 'New Link' ]
\end{code}

\begin{code}{}WAReddit>>renderHighestRankingLinksOn: html    html heading level: 2; with: 'Highest Ranking Links'. 
    html orderedList: [          self highestRankingLinks do: [ :each | self renderLink: each on: html ] ]
\end{code}

\begin{code}{}WAReddit>>renderLatestLinksOn: html    html heading level: 2; with: 'Latest Links'. 
    html orderedList: [         self latestLinks do: [ :each | self renderLink: each on: html ] ]
\end{code}

\begin{code}{}
WAReddit>>renderLink: link on: html    html listItem: [ html anchor url: link url; title: link url; with: link title.   
    html text: ' Posted ', (self class durationString: link posted), ' ago. '. 
    html text: link points asString, ' points. '. 
    html anchor 
         callback: [ self voteUp: link ]; 
         title: 'Vote this link up'; 
         with: 'Up'. 
    html space. 
    html anchor 
         callback: [ self voteDown: link ]; 
         title: 'Vote this link down'; with: 'Down' ]
\end{code}

Starting with the main \ct{renderContentOn:} method, the rendering of each section is delegated to its own method. Note how \ct{renderLink:on:} is used 2 times. For now, we're not yet implementing the 'New Link' action. Our rendering methods depend on 5 extra methods: \ct{highestRankingLinks}, \ct{latestLinks}, the class method \ct{durationString:} and the actions methods \ct{voteUp:} and \ct{voteDown:}. Only the first three are needed to render the page itself.

\sd{should explain Query class}
\begin{code}{}
WAReddit>>highestRankingLinks   | query | 
   query := (Query readManyOf: RedditLink)              orderBy: [ :each | each points descending ]; limit: 20; yourself.   ^ self session glorpSession execute: query
\end{code}

\begin{code}{}
WAReddit>>latestLinks   | query | 
   query := (Query readManyOf: RedditLink)              orderBy: [ :each | each created descending]; limit: 20; yourself.   ^ self session glorpSession execute: query
\end{code}

\begin{code}{}
WAReddit>>durationString: duration   ^ String streamContents: [ :stream | 
          | needSpace printer | 
          needSpace := false. 
          printer := [ :value :word |                         value isZero ifFalse: [ 
                              needSpace ifTrue: [ stream space ]. 
                              stream nextPutAll: (value pluralize: word). 
                              needSpace := true ] ].          printer value: duration days value: 'day'. 
          printer value: duration hours value: 'hour'. 
          printer value: duration minutes value: 'minute' ]
\end{code}

In \ct{highestRankingLinks} and \ct{latestLinks}, we explicitly build up and execute Query objects with some more advanced options. For duration string conversion we use the powerful \ct{pluralize} method. With these in place we can already render the page. Since we did not yet add any CSS styling, the result will look rather dull.

\begin{code}{}
WAReddit>>voteDown:link
    self session glorpSession
              inUnitOfWorkDo: [ :session | session register: link. 
                            link voteDown]
\end{code}



\begin{code}{}
WAReddit>>voteUp: link   self session glorpSession 
      inUnitOfWorkDo: [ :session | session register: link.  link voteUp ]
\end{code}

Voting links up or down is trivial, like with our database test, we only have to make sure to do the object modifications inside a Glorp unit of work and the actual SQL update will be done automatically.

\section{RedditFileLibrary}

To style our web app, we'll be reusing the CSS file from Reddit.lisp. This CSS code references one small GIF for its background gradient. We need to make sure our application makes use of the CSS file and that we serve the actual files. Seaside can serve these files in a couple of ways, we'll be using the \ct{FileLibrary} approach. This is a class where each resource served is implemented as a method.

We define a new class, subclass of WAFileLibrary, named \ct{RedditFileLibrary} will thus have 2 methods: \ct{mainCss} and \ct{bgGif}, returning a string and bytes respectively. These are long methods which are not our main focus so we do not list them here. Based on some naming conventions, Seaside will figure out what mime types to use.

\begin{code}{}
RedditFileLibrary>>updateRoot: anHtmlRoot    super updateRoot: anHtmlRoot. 
    anHtmlRoot title: 'Reddit.st'. 
    anHtmlRoot stylesheet url: (RedditFileLibrary urlOf: #mainCss)
\end{code}

By implementing the \ct{updateRoot:} hook method on \ct{WAReddit}, we can set our page title and CSS. Note again how everything happens in Smalltalk. To install a Seaside application, a class side \ct{initialize} method is typically used.

\begin{code}{}
RedditFileLibrary class>>initialize   (WAAdmin register: self asApplicationAt: 'reddit') 
        preferenceAt: #sessionClass put: RedditSession; 
        addLibrary: RedditFileLibrary
\end{code}


We register our application under the handler 'reddit' so its URL will become something like \ct{http://localhost:8080/reddit}. Then we tell it to use our custom session class and finally add our file library. We now have a nicely styled, working web app.

\section{WARedditLinkEditor}

One of Seaside's main advantages over other web application frameworks is its support for components. Especially for large and complex projects this makes a huge difference. We'll be introducing a new component to allow the user to enter the necessary information when adding a new link. Consider the difference between figure 1 and figure 2: when the user clicks the 'New Link' anchor, we'll add an editor just below (while hiding the 'New Link' anchor). The editor will have its own 'Save' and 'Cancel' buttons. Both of these will dismiss the editor, saving or cancelling the new link.


\begin{figure}\begin{center}
\includegraphics[width=8cm]{redditNewLink}
\caption{}
\end{center}
\end{figure}


How is Seaside's component model powerful ? As we will see next, the component is written without any knowledge of where it will be used. Its validation logic is independent. It is used just by embedding it and by wiring it to its user in a simple way. The subcomponent functions independently from its embedding parent while each keeps its own state: whether the component is visible or not, you can keep on voting links up or down, and doing so will not alter the contents of the component.


To prove our point, we'll be using yet another component inside our link editor: a simple CAPTCHA component. This will be implemented in the final section, but used here as a black box.
The first step is to make the necessary additions and modifications to \ct{WAReddit} to accommodate the link editor component. We add an instance variable called \ct{linkEditor} with its accessors.

\begin{code}{}
WAReddit>>renderContentOn: html     html heading: 'Reddit.st'. 
     html heading level: 3; with: 'In 10 elegant Smalltalk classes'. 
     self renderActionsOn: html. 
     !\textbf{self linkEditor notNil}!
        !\textbf{     ifTrue: [ html render: self linkEditor ].}!
     self renderHighestRankingLinksOn: html. 
     self renderLatestLinksOn: html
\end{code}


\begin{code}{}
WAReddit>>renderActionsOn: html
     html paragraph: [ 
     	html anchor callback: [ ]; with: 'Refresh'. 
     	self linkEditor isNil ifTrue: [
     	html anchor callback: [ self showNewLinkEditor ]; with: 'New Link' ] ]
\end{code}


\begin{code}{}
WAReddit>>showNewLinkEditor     self linkEditor: WARedditLinkEditor new. 
     self linkEditor onAnswer: [ :answer |          answer ifTrue: [ 
               self session glorpSession inUnitOfWorkDo: [ :session |                    session register: self linkEditor createLink ] ]. 
          self linkEditor: nil ]
\end{code}


\begin{code}{}
WAReddit>>children    ^ self linkEditor notNil 
        ifTrue: [ Array with: self linkEditor ] 
        ifFalse: [ super children ]
\end{code}

There are 2 possible states: either we have a link editor subcomponent or not. So the main \ct{renderContentOn:} method conditionally asks the link editor to render itself. Likewise, in \ct{renderActionsOn:} the 'New Link' anchor is only rendered when there is no link editor yet.
In the \ct{showNewLinkEditor} action method we instantiate our subcomponent and hook it up. We could have reused just one instance, creating a new one is easier and clearer. The wiring is done by supplying a block to \ct{onAnswer:}. A component can answer a value, in our case true or false for save or cancel respectively. So when the link editor answers true, we save a new link object and hide the editor.
In Seaside, the \ct{children} method is a hook method that has to be implemented to list all subcomponents. Again this happens conditionally.
We can now implement the component itself: \ct{WARedditLinkEditor} is a subclass of \ct{WAComponent} with 3 instances variables and their accessors: \ct{url}, \ct{title} and \ct{capcha}.



\begin{code}{}
WARedditLinkEditor>>renderContentOn: html	html form: [		html paragraph: 'Please enter a URL and title for the link that you want to add:'. 
		html textInput size: 48; title: 'The URL of the new link'; on: #url of: self. 
		html textInput size: 48; title: 'The title of the new link'; on: #title of: self. 
		html render: self captcha.		html submitButton on: #cancel of: self. 
		html submitButton on: #save of: self ]
\end{code}


\begin{code}{}
WARedditLinkEditor>>initialize	super initialize. 
	self url: 'http://'; 
		title: 'title'; 
		captcha: WARedditCaptcha new
\end{code}


\begin{code}{}
WARedditLinkEditor>>children	^ Array with: self captcha
\end{code}

\begin{code}{}
WARedditLinkEditor>>updateRoot: anHtmlRoot	super updateRoot: anHtmlRoot. 
	anHtmlRoot title: 'Reddit.st - Submit a new link'. 
	anHtmlRoot stylesheet url: (RedditFileLibrary urlOf: #mainCss)
\end{code}


\begin{code}{}
WARedditLinkEditor>>cancel	self answer: false
\end{code}

\begin{code}{}
WARedditLinkEditor>>save	self isUrlMissing ifTrue: [ ^ self inform: 'Please enter an URL' ]. 
	self isTitleMissing ifTrue: [ ^ self inform: 'Please enter a title' ]. 
	self captcha isSolved ifFalse: [ ^ self inform: 'Please answer the correct sum using digits' ]. 
	self isUrlValid ifFalse: [ ^ self inform: 'The URL you entered did not resolve' ]. 
	self answer: true
\end{code}



\begin{code}{}
WARedditLinkEditor>>isTitleMissing	^ self title isNil or: [ self title isEmpty or: [ self title = 'title' ] ]
\end{code}

\begin{code}{}WARedditLinkEditor>>isUrlMissing	^ self url isNil or: [ self url isEmpty or: [ self url = 'http://' ] ]
\end{code}

\begin{code}{}WARedditLinkEditor>>isUrlValid	^ (WebClient httpGet: self url) isSuccess
\end{code}

\begin{code}{}WARedditLinkEditor>>createLink	^ RedditLink withUrl: self url title: self title
\end{code}

Most of the code should be familiar by now. New is how \ct{cancel} and \ct{save} use \ct{answer:} to return to whoever called upon this component. Before \ct{save} returns successfully, a number of validation tests are done. When one of these tests fails, a message is shown and the operation is aborted. The \ct{isUrlValid} method actually tries to resolve the URL. Finally, \ct{createLink} instantiates a new \ct{RedditLink} instance based on the valid fields entered by the user. Note how the CAPTCHA is used as a true component.

\section{WARedditCaptcha: A last Web Component}

The last and simplest web component is a CAPTCHA that presents a simple addition in words. This component does not need answer logic. The class \ct{WARedditCaptcha} is again a subclass of \ct{WAComponent} with the following instance variables and accessors: \ct{x}, \ct{y}, and \ct{sum}.


\begin{code}{}
WARedditCaptcha>>renderContentOn: html	self x: 10 atRandom. 
	self y: 10 atRandom. 
	html paragraph: 'CAPTCHA: How much is ', 
		self x asWords, ' plus ', self y asWords, ' ?'. 
	html textInput 
		title: 'This functions as a CAPTCHA, type the answer using digits'; 
		on: #sum of: self
\end{code}

\begin{code}{}
WARedditCaptcha>>initialize	super initialize. 
	self x: 0; y: 0; sum: 0
\end{code}

\begin{code}{}
WARedditCaptcha>>isSolved	^ self sum asInteger = (self x + self y)
\end{code}

Each time the CAPTCHA is rendered, \ct{x} and \ct{y} get a new random value between 1 and 10. Next, the addition is presented in words. The \ct{isSolved} method checks if the user answered correctly.

\section{Conclusion}

The source code discussed in this chapter is available the SqueakSource project called ADayAtTheBeach. Look for the package called Reddit.

Measuring the quality of the design is often a challenge. We believe that having classes with clear responsibilities. This is what we applied when designing such little application. Reddit.st consists of 10 classes for a total of 8 class methods and 75 instance methods. More than half are just one (1) line long, the rest averages just a few lines.



%=========================================================
\ifx\wholebook\relax\else
    \bibliographystyle{jurabib}
    \nobibliography{scg}
    \end{document}
\fi
%=========================================================



%%% Local Variables: 
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: Lint.tex
%%% TeX-PDF-mode:
%%% End:
