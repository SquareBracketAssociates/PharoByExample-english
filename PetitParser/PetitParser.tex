% $Author$
% $Date$
% $Revision$

% HISTORY:
% Chapter started by Damien C (2011-07-11)
% 2011-09-11 - Migrated to PharoBox: svn checkout https://XXX@scm.gforge.inria.fr/svn/pharobooks/PharoByExampleTwo-Eng
% could change returns by answers 


%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
%=================================================================
%\renewcommand{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================
\chapter{PetitParser}
\chalabel{petitparser}

\chapterauthor{\authorlukas{}}

Building parsers to analyse data is a common tasks in software development. In this chapter we present a powerful parser frameworks named PetitParser. And contrary to its name, PetitParser is a really powerful parsing framework combining several technologies (scannerless parsers, parser combinators..).
PetitParser is written by Lukas Renggli  as part of his work on the Helvetia system but it can be used as a standalone tool.



\section{Writing Parsers with PetitParser}

 PetitParser is a parsing framework different
to many other popular parser generators. For example, it is not table
based such as SmaCC or ANTLR. Instead it uses a unique combination of
four alternative parser methodologies: scannerless parsers, parser
combinators, parsing expression grammars and packrat parsers. As such
PetitParser is more powerful in what it can parse and it arguably fits
better the dynamic nature of Smalltalk. Let's have a quick look at
these four parser methodologies:

\begin{description}
\item[Scannerless Parsers] combine what is usually done by two
  independent tools (scanner and parser) into one. This makes writing
  a grammar much simpler and avoids common problems when grammars are
  composed.

\item[Parser Combinators] are building blocks for parsers modeled as a
  graph of composable objects; they are modular and maintainable, and
  can be changed, recomposed, transformed and reflected upon.

\item[Parsing Expression Grammars] (PEGs) provide ordered choice. Unlike in
  parser combinators, the ordered choice of PEGs always follows the
  first matching alternative and ignores other alternatives. Valid
  input always results in exactly one parse-tree, the result of a
  parse is never ambiguous.

\item[Packrat Parsers] give linear parse time guarantees and avoid
  common problems with left-recursion in PEGs.
\end{description}


\subsection{Loading PetitParser}

Enough theory, let's get started. PetitParser is developed in Pharo,
but is also available on other Smalltalk platforms. A ready made image
can be
downloaded.\footnote{\url{http://hudson.lukas-renggli.ch/job/PetitParser/lastSuccessfulBuild/artifact/petitparser}}
To load PetitParser into an existing image evaluate the following
Gofer expression:

\begin{code}{}
Gofer new
  renggli: 'petit';
  package: 'PetitParser';
  package: 'PetitTests';
  load.
\end{code}

There are other packages in the same repository that provide
additional features, for example PetitSmalltalk is a Smalltalk
grammar, PetitXml is an XML grammar, PetitJson is a JSON grammar,
PetitAnalyzer provides functionality to analyze and transform
grammars, and PetitGui is a Glamour IDE (see \charef{glamour}) for
writing complex grammars. We are not going to use any of these
packages for now.

More information on how to get PetitParser can be found on the website
of the
project.\footnote{\url{http://scg.unibe.ch/research/helvetia/petitparser}}

\subsection{Writing a Simple Grammar}

Writing grammars with PetitParser is as simple as writing Smalltalk
code. For example to write a grammar  parsing identifiers that
start with a letter followed by zero or more letter or digits is
defined as follows. In a workspace we evaluate:

\begin{code}{}
identifier := #letter asParser , #word asParser star.  
\end{code}

If you inspect the object identifier you'll notice that it is an
instance of a PPSequenceParser. This is because the \#, operator
(comma) created a sequence of a letter and a zero or more word
character parser. If you dive further into the object you notice the
following simple composition of different parser objects:

\begin{code}{}
PPSequenceParser (this parser accepts a sequence of parsers)
    PPPredicateObjectParser (this parser accepts a single letter)
    PPRepeatingParser (this parser accepts zero or more instances of another parser)
       PPPredicateObjectParser (this parser accepts a single word character)  
\end{code}

\subsection{Parsing Some Input}

To actually parse a string (or stream) we can use the method \ct{#parse:}:

\begin{code}{@Test}
identifier parse: 'yeah'.          --> #($y #($e $a $h))
identifier parse: 'f123'.           --> #($f #($1 $2 $3))
\end{code}

While it seems odd to get these nested arrays with characters as a
return value, this is the default decomposition of the input into a
parse tree. We'll see in a while how that can be customized.

If we try to parse something invalid we get an instance of
\ct{PPFailure} as an answer:

\begin{code}{@Test}
identifier parse: '123'.           --> letter expected at 0
\end{code}

Instances of \ct{PPFailure} are the only objects in the system that
answer with true when you send the message \ct{#isPetitFailure}.
Alternatively you can also use \ct{#parse:onError:} to throw an
exception in case of an error:

\begin{code}{}
identifier
   parse: '123'
   onError: [ :msg :pos | self error: msg ].  
\end{code}

If you are only interested if a given string (or stream) matches or
not you can use the following constructs:

\begin{code}{@Test}
identifier matches: 'foo'.         --> true
identifier matches: '123'.        --> false
\end{code}

Furthermore to find all matches in a given input string (or stream)
you can use:

\begin{code}{}
identifier matchesIn: 'foo 123 bar12'.  
\end{code}

Similarly, to find all the matching ranges in the given input string
(or stream) you can use:

\begin{code}{}
identifier matchingRangesIn: 'foo 123 bar12'.  
\end{code}

\subsection{Different Kinds of Parsers}

PetitParser provide a large set of ready-made parser that you can
compose to consume and transform arbitrarily complex languages. The
terminal parsers are the most simple ones. We've already seen a few of
those:

\begin{tabular}{ll}
\textbf{Terminal Parsers} 	& \textbf{Description}\\
\midrule
\$a asParser      & 	Parses the character \$a.\\
'abc' asParser   & 	Parses the string 'abc'.\\
\#any asParser    &	Parses any character.\\
\#digit asParser  & 	Parses the digits 0..9.\\
\#letter asParser & 	Parses the letters a..z and A..Z.\\
\end{tabular}

The class side of \ct{PPPredicateObjectParser} provides a lot of other
factory methods that can be used to build more complex terminal
parsers. To use them, send the message \ct{asParser} to a symbol
containing the name of the factory method (such as \ct{#punctuation}
\ct{asParser}).

The next set of parsers are used to combine other parsers together:

\begin{tabular}{lp{.6\textwidth}}
\textbf{Parser Combinators} & \textbf{Description}\\
\midrule
p1 , p2     & Parses p1 followed by p2 (sequence).\\
p1 / p2     & Parses p1, if that doesn't work parses p2 (ordered choice).\\
p star      & Parses zero or more p.\\
p plus      & Parses one or more p.\\
p optional  & Parses p if possible.\\
p and 	    & Parses p but does not consume its input.\\
p not       & Parses p and succeed when p fails, but does not consume its input.\\
p end 	    & Parses p and succeed at the end of the input.  \\
\end{tabular}

So instead of using the \ct{#word} predicated we could have written
our identifier parser like this:

\begin{code}{}
identifier := #letter asParser , (#letter asParser / #digit asParser) star.
\end{code}

To attach an action or transformation to a parser we can use the following methods:

\begin{tabular}{ll}
\textbf{Action Parsers} & 	\textbf{Description}\\
\midrule
p ==> aBlock & 	Performs the transformation given in aBlock.\\
p flatten    & 	Creates a string from the result of p.\\
p token      &	Creates a token from the result of p.\\
p trim       &	Trims whitespaces before and after p.
\end{tabular}

To return a string of the parsed identifier, we can modify our parser
like this:

\begin{code}{}
identifier := (#letter asParser , (#letter asParser / #digit asParser) star) flatten.
\end{code}

These are the basic elements to build parsers. There are a few more
well documented and tested factory methods in the operations protocol
of PPParser. If you want browse that protocol.

\subsection{Writing a More Complicated Grammar}

Now we are able to write a more complicated grammar for evaluating
simple arithmetic expressions. Within a workspace we start with the
grammar for a number (actually an integer):

\begin{code}{@Test}
number :=  #digit asParser plus token trim ==> [ :token | token value asNumber ].
number parse: '123'             --> 123
\end{code}

Then we define the productions for addition and multiplication in
order of precedence. Note that we instantiate the productions as
\ct{PPDelegateParser} upfront, because they recursively refer to each
other. The method \ct{#setParser:} then resolves this recursion.

\begin{code}{}
term := PPDelegateParser new.
prod := PPDelegateParser new.
prim := PPDelegateParser new.
 
term setParser: (prod , $+ asParser trim , term ==> [ :nodes | nodes first + nodes last ]) / prod.
prod setParser: (prim , $* asParser trim , prod ==> [ :nodes | nodes first * nodes last ]) / prim.
prim setParser: ($( asParser trim , term , $) asParser trim ==> [ :nodes | nodes second ]) / number.
\end{code}

To make sure that our parser consumes all input we wrap it with the
\ct{end} parser into the \ct{start} production:

\begin{code}{}
start := term end.
\end{code}

That's it, now we can test our parser and evaluator:

\begin{code}{@Test}
start parse: '1 + 2 * 3'.       --> 7
start parse: '(1 + 2) * 3'.     --> 9
\end{code}

\section{Composite Grammars with PetitParser}

In the previous section we saw the basic principles of PetitParser and
gave some introductory examples. In this section we are going to
present a way to define more complicated grammars. We continue where
we left off the last time, with the expression grammar.

Writing parsers as a script as we did previously can be cumbersome,
especially if grammar productions that are mutually recursive and
refer to each other in complicated ways. Furthermore a grammar
specified in a single script makes it unnecessary hard to reuse
specific parts of that grammar. Luckily there is
\ct{PPCompositeParser} to the rescue.

\section{Defining the Grammar}

As an example let's create a composite parser using the same
expression grammar we built in the last section:

\begin{code}{}
PPCompositeParser subclass: #ExpressionGrammar
   instanceVariableNames: ''
   classVariableNames: ''
   poolDictionaries: ''
   category: 'PetitTutorial'
\end{code}

Again we start with the grammar for an integer number. Define the
method \ct{number} in \ct{ExpressionGrammar} as follows:

\begin{code}{}
ExpressionGrammar>>number
   ^ #digit asParser plus token trim ==> [ :token | token value asNumber ]
\end{code}

Every production in \ct{ExpressionGrammar} is specified as a method
that returns its parser. Next we define the productions \ct{term},
\ct{prod}, and \ct{prim}. Productions refer to each other by reading
the respective instance variable of the same name. This is important
to be able to create recursive grammars. The instance variables
themselves are typically not written to as PetitParser takes care to
initialize them for you automatically. We let Pharo automatically add
the necessary instance variables as we refer to them for the first
time.

\begin{code}{}
ExpressionGrammar>>term
   ^ add / prod

ExpressionGrammar>>add
   ^ prod , $+ asParser trim , term

ExpressionGrammar>>prod
   ^ mul / prim

ExpressionGrammar>>mul
   ^ prim , $* asParser trim , prod

ExpressionGrammar>>prim
   ^ parens / number

ExpressionGrammar>>parens
   ^ $( asParser trim , term , $) asParser trim
\end{code}

Contrary to our previous implementation we do not define the
production actions yet; and we factor out the parts for addition
(\ct{add}), multiplication (\ct{mul}), and parenthesis (\ct{parens})
into separate productions. This will give us better reusability later
on. Usually, production methods are categorized in a protocol named
\ct{grammar} (which can be refined into more specific protocol names
when necessary such as \ct{grammar-literals}).

Last but not least we define the starting point of the expression
grammar. This is done by overriding start in the
\ct{ExpressionGrammar} class:

\begin{code}{}
ExpressionGrammar>>start
   ^ term end
\end{code}

Instantiating the \ct{ExpressionGrammar} gives us an expression parser
that returns a default abstract-syntax tree:

\begin{code}{@Test}
parser := ExpressionGrammar new.
parser parse: '1 + 2 * 3'.       --> #(1 $+ #(2 $* 3))
parser parse: '(1 + 2) * 3'.     --> #(#($( #(1 $+ 2) $)) $* 3)
\end{code}

\section{Defining the Evaluator}

Now that we have defined a grammar we can reuse this definition to
implement an evaluator. To do this we create a subclass of
\ct{ExpressionGrammar} called \ct{ExpressionEvaluator}.

\begin{code}{}
ExpressionGrammar subclass: #ExpressionEvaluator
   instanceVariableNames: ''
   classVariableNames: ''
   poolDictionaries: ''
   category: 'PetitTutorial'
\end{code}

We then redefine the implementation of \ct{add}, \ct{mul} and
\ct{parens} with our evaluation semantics:

\begin{code}{}
ExpressionEvaluator>>add
   ^ super add ==> [ :nodes | nodes first + nodes last ]

ExpressionEvaluator>>mul
   ^ super mul ==> [ :nodes | nodes first * nodes last ]

ExpressionEvaluator>>parens
   ^ super parens ==> [ :nodes | nodes second ]
\end{code}

The evaluator is now ready to be tested:

\begin{code}{@Test}
parser := ExpressionEvaluator new.
parser parse: '1 + 2 * 3'.       --> 7
parser parse: '(1 + 2) * 3'.     --> 9
\end{code}

Similarly a pretty printer can be defined by subclassing
\ct{ExpressionGrammar}:

\begin{code}{@Test}
parser := ExpressionPrinter new.
parser parse: '1+2 *3'.          --> '1 + 2 * 3'
parser parse: '(1+ 2 )* 3'.      --> '(1 + 2) * 3'
\end{code}

This is done with the following code:

\begin{code}{}
ExpressionGrammar subclass: #ExpressionPrinter
  instanceVariableNames: ''
  classVariableNames: ''
  poolDictionaries: ''
  category: 'PetitTutorial'

ExpressionPrinter>>add
  ^ super add ==> [:nodes | nodes first, ' + ', nodes third]

ExpressionPrinter>>mul
  ^ super mul ==> [:nodes | nodes first, ' * ', nodes third]

ExpressionPrinter>>number
  ^ super number ==> [:num | num printString]

ExpressionPrinter>>parens
  ^ super parens ==> [:node | '(', node second, ')']
\end{code}

\section{Testing a Grammar}

Testing a grammar is done by subclassing \ct{PPCompositeParserTest} as
follows:

\begin{code}{}
PPCompositeParserTest subclass: #ExpressionGrammarTest
  instanceVariableNames: ''
  classVariableNames: ''
  poolDictionaries: ''
  category: 'PetitTutorial'
\end{code}

Then, the parser class should be defined in the corresponding method:

\begin{code}{}
ExpressionGrammarTest>>parserClass
  ^ ExpressionGrammar
\end{code}

Tests can then be written in separate methods:

\begin{code}{}
ExpressionGrammarTest>>testNumber
  self parse: '123 ' rule: #number.

ExpressionGrammarTest>>testAdd
  self parse: '123+77' rule: #add.
\end{code}

Testing the evaluator and printer is similarly easy:

\begin{code}{}
ExpressionGrammarTest subclass: #ExpressionEvaluatorTest
  instanceVariableNames: ''
  classVariableNames: ''
  poolDictionaries: ''
  category: 'PetitTutorial'

ExpressionEvaluatorTest>>parserClass
  ^ ExpressionEvaluator 

ExpressionEvaluatorTest>>testAdd
  super testAdd.
  self assert: result equals: 200

ExpressionEvaluatorTest>>testNumber
  super testNumber.
  self assert: result equals: 123

ExpressionGrammarTest subclass: #ExpressionPrinterTest
  instanceVariableNames: ''
  classVariableNames: ''
  poolDictionaries: ''
  category: 'PetitTutorial'

ExpressionPrinterTest>>parserClass
  ^ ExpressionPrinter 

ExpressionPrinterTest>>testAdd
  super testAdd.
  self assert: result equals: '123 + 77'

ExpressionPrinterTest>>testNumber
  super testNumber.
  self assert: result equals: '123'
\end{code}

\subsection{Chapter Conclusion}

This concludes our tutorial of PetitParser. Please note that this
tutorial is not meant to give an exhaustive overview of PetitParser,
but is merely intended to introduce the reader to the usage and to our
intent for our approach. For a more extensive view of PetitParser, its
concepts and implementation, the Moose
book\footnote{\url{http://www.themoosebook.org/book}} and Lukas
Renggli's PhD have a dedicated chapter dedicated.

%=============================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi
%=============================================================




%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End:
% LocalWords:  subclassing
