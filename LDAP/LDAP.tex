% $Author: ducasse $
% $Date: 2009-08-24 10:17:33 +0200 (Mon, 24 Aug 2009) $
% $Revision: 28563 $

% 2011-09-11 - Migrated to PharoBox: svn checkout https://XXX@scm.gforge.inria.fr/svn/pharobooks/PharoForTheEnterprise-Eng



%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
%=================================================================
%\renewmessage{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================
%\newcommand{\apl}[1]{\nnbb{Alain}{#1}} % Plantec
%\newcommand{\setbrowser}{\textit{Settings Browser}\xspace}

\chapter{Using LDAP}
\chapterauthor{\authorolivier{}}

\section{Pr\'esentation du protocole LDAP}

Le terme LDAP (Lightweight Directory Access Protocol) d\'esigne un protocole permettant l'acc\`es en lecture et en \'ecriture \`a des annuaires d'entreprises. Ces bases de donn\'ees, optimis\'ees pour la lecture, sont des r\'ef\'erentiels d'informations sur les composants d'une organisation (individus, ressources, organisation fonctionnelle). Le plus souvent, les annuaires LDAP sont exploit\'es pour l'identification des utilisateurs aux services de messagerie ou aux applications intranet. Dans le cadre du d\'eveloppement d'applications e-business, les annuaires LDAP sont des composants techniques essentiels et critiques : il est le plus souvent obligatoire d'interfacer un logiciel avec ces annuaires.

Pharo dispose de diff\'erents frameworks pour le d\'eveloppement web (Seaside, AIDAweb, etc.) ainsi que pour l'acc\`es aux bases de donn\'ees (DBXTalk). Quelle est la situation avec LDAP ? Peut on cr\'eer des applications capables d'identifier un utilisateur ou de r\'ecup\'erer des informations dans un annuaire LDAP ?

\subsection{Installation de LDAPlayer}

Pour communiquer avec un annuaire LDAP, Pharo dispose de la biblioth\`eque de classes LDAPlayer cr\'e\'ee par Ragnar Hojland Espinosa. Elle disponible via Squeaksource \`a l'adresse suivante: \url{http://www.squeaksource.org/LDAPlayer/}.


Pour l'installer, lancez Pharo et d\'efinissez un nouveau d\'ep\^ot HTTP \`a l'aide de Monticello.
\begin{code}{}
MCHttpRepository 
	location: 'http://www.squeaksource.com/LDAPlayer' 
	user: '' password: ''
\end{code}
\sd{better use a Gofer expression!}

\section{Les principales classes}

La plupart des classes que nous allons d\'ecouvrir sont stock\'ees dans le paquet LDAP-Core. Une connexion \`a un annuaire LDAP instancie un objet LDAPConnection. Des requ\^etes vers l'annuaire retourne des objets LDAPResult. La classe LDAPAttrModifier fournie plusieurs m\'ethodes de classe permettant de modifier les attributs d'une entr\'ee de l'annuaire. La classe LDAPFilter a pour finalit\'e de d\'efinir des filtres de recherche et de limiter ainsi le volume d'informations retourn\'e au client.




\paragraph{D\'efinir une connexion.}
Cr\'eons une nouvelle cat\'egorie nomm\'ee 'AppLDAP' pour d\'ecouvrir les fonctionnalit\'es de LDAPlayer. Notre objectif est de mettre en situation les principales fonctionnalit\'es de la biblioth\`eque LDAPlayer et de vous permettre de les r\'eutiliser facilement dans une v\'eritable application. Cette cat\'egorie contient une classe unique nomm\'ee 'Ldap' qui renfermera l'ensemble de nos m\'ethodes d'instances.


Pour d\'efinir la connexion, nous allons utiliser cinq variables d'instance qui repr\'esentent le nom du serveur LDAP (hostname), le port TCP de connexion (port), le compte utilisateur (bindDN), le mot de passe de l'utilisateur (password) et la base de la recherche (baseDN) qui sera utilis\'ee pour construire un DN (Distinguished Name).

\begin{code}{}
Object subclass: #Ldap 
	instanceVariableNames: 'baseDN bindDN hostname password port'
	classVariableNames: '' 
	poolDictionaries: '' 
	category: 'Ldap'
\end{code}

\sd{I do not like DN}
\sd{We should explain that basically everything resolves around the call. }
\begin{code}{}
Ldap>>baseDN 
	^ baseDN
Ldap>>baseDN: anObject 
	baseDN := anObject
Ldap>>bindDN 
	^bindDN
Ldap>>bindDN: anObject 
	bindDN := anObject
Ldap>>hostname 
	^hostname
Ldap>>hostname: anObject 
	hostname := anObject
Ldap>>password 
	^password
Ldap>>password: anObject 
	password := anObject
Ldap>>port 
	^ port
Ldap>>port: anObject 
	port := anObject
\end{code}



Pour initialiser ces variables d'instances, cr\'eons une m\'ethode initialize dans le protocole initalize-release.

\begin{code}{}
Ldap>>initialize 
	super initialize. 
	self hostname: 'ldap.mondomaine.org'. 
	self port: 389. 
	self bindDN: 'cn=admin,dc=domaine,dc=org'. 
	self password: 'mysecretpassword'. 
	self baseDN: 'ou=people,dc=domaine,dc=org'.
\end{code}

\sd{what is ou=people,dc=domaine,dc=org'?}
\sd{is there a ldap that we could query to try}

Nous avons \'egalement besoin d'une variable d'instance permettant de sauvegarder un objet d\'ecrivant la connexion vers l'annuaire LDAP.

\begin{code}{}
Object subclass: #Ldap 
	instanceVariableNames: 'connection baseDN bindDN hostname password port' 
	classVariableNames: '' 
	poolDictionaries: '' 
	category: 'Ldap'
\end{code}

Bien \'evidemment, nous avons besoin de deux accesseurs pour \'ecrire et lire dans cette variable d'instance. Cr\'eons un protocole \ct{'accessing'} et d\'efinissons les deux m\'ethodes suivantes :
\begin{code}{}
Ldap>>connection 
	^ connection
Ldap>>connection: anObject 
	connection := anObject
\end{code}


Nous pouvons maintenant d\'efinir un protocole \ct{'action'} qui contiendra l'ensemble des m\'ethodes d'instances ayant une interaction avec l'annuaire LDAP. Commen\c cons par d\'efinir une m\'ethode \ct{connect} qui assure la connexion et initialise la variable d'instance \ct{connection}.

\begin{code}
Ldap>>connect 
	| req | 
	self connection: (LDAPConnection to: self hostname port: self port). 
	req := connection bindAs: self bindDN credentials: self password. 
	req wait. 
	^ self.
\end{code}

\sd{je ne comprends pas pourquoi on utilise le self connection: et apres on accede directement}


Pour travailler proprement, il nous faut \'egalement une m\'ethode permettant de d\'econnecter notre application une fois que la connexion \`a l'annuaire LDAP n'est plus utile. Pour cela, d\'efinissez simplement une m\'ethode 'disconnect' qui sera charg\'ee de cette t\^ache.

\begin{code}{}
Ldap>>disconnect 
	self connection disconnect.
\end{code}


Est-ce que cela fonctionne ? Un petit test rapide pour v\'erifier. Ouvrons le Workspace et ex\'ecutons le code suivant :

\begin{code}{}
	ann := Ldap new connect. 
	ann disconnect.
\end{code}

Si tout c'est bien pass\'e, f\'elicitations ! Notre application s'est connect\'e \`a votre annuaire LDAP. Passons \`a la suite et tentons de r\'ecup\'erer des informations.


\subsection{Chercher des informations}



Nous pouvons maintenant essayer de lire des donn\'ees dans notre annuaire LDAP. Pour cela, nous allons d\'efinir une premi\`ere m\'ethode 'searchAll', tr\`es simple, qui r\'ecup\`ere l'int\'egralit\'e des entr\'ees \`a partir du DN de base.
Facile n'est-ce pas ? Par contre, ce n'est pas une m\'ethode tr\`es recommandable si votre annuaire contient plusieurs milliers d'entr\'ees. Lorsque l'on fait une recherche dans un LDAP, on travaille plut\^ot avec des filtres afin de limiter le volume d'information r\'ecup\'er\'e par l'application cliente.
D\'efinissons une m\'ethode 'search' recherchant les entr\'ees pour lesquelles un attribut \`a une certaine valeur.


\begin{code}{}
Ldap>>searchAll 
	| req result |
	req := self connection 
				newSearch: self baseDN 
				scope: (LDAPConnection wholeSubtree) 
				deref: (LDAPConnection derefNever) 
				filter: nil 
				attrs: nil 
				wantAttrsOnly: false.
	^req result
\end{code}

Dans le workspace, nous pouvons maintenant lire et afficher ces informations. A titre d'exemple, nous n'affichons que l'attribut \ct{cn} :

\begin{code}{}
	| ann result |
	ann := Ldap new connect. 
	result := ann searchAll. 
	result do: [ :each |
		Transcript show: (each attrAt: #cn); cr ]. 
	ann disconnect.
\end{code}

\begin{code}{}
Ldap>>search: aValue attribute: aAttribute 
	| req result |
	req := self connection 
		newSearch: self baseDN 
		scope: (LDAPConnection wholeSubtree) 
		deref: (LDAPConnection derefNever) 
		filter: (LDAPFilter with: aAttribute equalTo: aValue) 
		attrs: nil 
		wantAttrsOnly: false.
	^req result
\end{code}


Dans le Workspace, nous pouvons maintenant rechercher les entr\'ees pour lesquelles le champ \ct{'equipe'} est \'egal \`a 'INFORMATIQUE'.

\begin{code}{}
	| ann result |
	ann := Ldap new connect. 
	result := ann search: 'INFORMATIQUE' attribute: 'equipe'. 
	result do: [ :each |
		Transcript show: (each attrAt: #cn); cr ]. 
	ann disconnect.
\end{code}

\subsection{Modifier des attributs}

La modification des attributs consiste \`a ajouter une valeur \`a un attribut ou \`a supprimer un attribut. Tout d'abord, d\'efinissons une m\'ethode pour ajouter un attribut \`a une entr\'ee existante. Cette m\'ethode re\c coit trois param\`etres qui sont l'identifiant unique d'une entr\'ee dans l'annuaire (DN) construit \`a partir de l'UID (User ID) et du DN de base, le nom du param\`etre modifi\'e et la valeur affect\'e \`a ce param\`etre.

\begin{code}{}
Ldap>>addValueTo: aDN attribute: aAttribute with: aValue 
	| req ops |
	ops := { LDAPAttrModifier addTo: aAttribute values: { aValue }.}.
	req := connection modify: (aDN, ',', self baseDN) with: ops. req wait.
\end{code}

Essayons maintenant cette m\'ethode dans le Workspace en ajoutant une valeur \`a l'attribut \ct{'givenName'} de l'utilisateur \ct{'dupont'} :

\begin{code}{}
	| ann | 
	ann := Ldap new connect. 
	ann addValueTo: 'UID=dupont' attribute: 'givenName' with: 'Alfred'. 
	ann disconnect.
\end{code}

Nous allons ensuite cr\'eer une m\'ethode permettant de changer la valeur d'un attribut. Cette m\'ethode re\c coit trois param\`etres qui sont le DN de l'entr\'ee devant \^etre modifi\'ee, le nom de l'attribut et la valeur affect\'ee.

\begin{code}{}
Ldap>>changeValueOf: aDN attribute: aAttribute with: aValue 
	| req ops |
	ops := { LDAPAttrModifier set: aAttribute to: { aValue }}. 
	req := connection modify: (aDN, ',', self baseDN) with: ops. 
	req wait.
\end{code}


L'exemple suivant modifie l'attribut 'Statut' de l'utilisateur 'dupont'.
\begin{code}{}
	| ann |
	ann := Ldap new connect. 
	ann changeValueOf: 'UID=dupont' attribute: 'Statut' with: 'programmer'. 
	ann disconnect.
\end{code}

Cr\'eons maintenant une m\'ethode qui supprime un attribut dans le LDAP. Cette m\'ethode re\c coit un DN en param\`etre afin de sp\'ecifier l'entr\'ee concern\'ee par la suppression de l'attribut.

\begin{code}{}
Ldap>>deleteAttribute: aAttribute from: aDN 
	
	| req ops |
	ops := { LDAPAttrModifier del: aAttribute}. 
	req := connection modify: (aDN, ',', self baseDN) with: ops. 
	req wait.
\end{code}

Nous pouvons maintenant supprimer l'entr\'ee \ct{'stage'} pour l'UID \ct{'dupont'} :

\begin{code}{}
	| ann |
	ann := Ldap new connect. 
	ann deleteAttribute: 'stage' from: 'UID=dupont'. 
	ann disconnect.
\end{code}
Il est \'egalement possible d'effacer un attribut en fonction de sa valeur. Ecrivons une m\'ethode \ct{deleteAttribute:value:from:} qui r\'ealise cela :

\begin{code}{}
Ldap>>deleteAttribute: aAttribute value: aValue from: aDN | req ops |
	ops := { LDAPAttrModifier delFrom: aAttribute values: { aValue }.}.
	req := connection modify: (aDN, ',', self baseDN) with: ops. 
	req wait.
\end{code}

L'exemple suivant supprime l'attribut \ct{'givenName'} si sa valeur est \'egale \`a \ct{'Alfred'}. La recherche est bas\'ee ici sur un DN constitu\'e d'un UID utilisateur et du DN de base.

\begin{code}{}
	| ann |
	ann := Ldap new connect. 
	ann deleteAttribute: 'givenName' value: 'Alfred' from: 'UID=dupont'. 
	ann disconnect.
\end{code}


\subsection{Cr\'eer une entr\'ee dans l'annuaire}

Si nous voulons ajouter un nouvel \'el\'ement \`a notre annuaire, il nous faut cr\'eer une nouvelle entr\'ee. Nous allons cr\'eer une m\'ethode \ct{createEntry:with:} qui re\c coit en param\`etre un UID ainsi qu'une collection contenant les informations devant \^etre ins\'er\'ees dans la nouvelle entr\'ee.

\begin{code}{}
Ldap>>createEntry: aUID with: aCollection 
	| req |
	req := self connection addEntry: aUID attrs: aCollection. 
	req wait.
\end{code}

Nous pouvons alors ajouter une nouvelle entr\'ee de type \ct{'inetOrgPerson'} pour l'utilisateur \ct{'dupont'}. Les attributs \ct{'cn'} et \ct{'sn'} sont fix\'es \`a \ct{'dupont'} \'egalement. \sd{c'est quoi cn et sn?}

\begin{code}{}
	| ann attrs |
	attrs := Dictionary new 
				at: 'objectClass' 
				put: (OrderedCollection new add: 'inetOrgPerson'; yourself); 
				at: 'cn' put: 'dupont'; 
				at: 'sn' put: 'dupont'; yourself.
	ann := Ldap new connect. 
	ann createEntry: ('uid=dupont,' , ann baseDN) with: attrs.
	ann disconnect.
\end{code}

\subsection{Supprimer une entr\'ee dans l'annuaire}
Effacer une entr\'ee dans l'annuaire est simple et rapide. Attention aux mauvaises manoeuvres ! 
Pour cela, nous pouvons d\'efinir une m\'ethode \ct{deleteEntry:} recevant en param\`etre l'UID de l'entr\'ee qui sera effac\'ee.

\begin{code}{}
Ldap>>deleteEntry: aUID 

	| req | 
	req := self connection delEntry: (aUID , ',' , self baseDN). 
	req wait.
\end{code}

Essayons notre m\'ethode en supprimant dans l'annuaire, l'entr\'ee pr\'ec\'edemment cr\'e\'ee pour l'utilisateur \ct{'dupont'} :

\begin{code}{}
| ann |
ann := Ldap new connect. 
ann deleteEntry: 'uid=dupont'. 
ann disconnect.
\end{code}

\section{Pr\'esentation des classes principales}


\section{Conclusion}
Nous venons de d\'ecouvrir les principales fonctionnalit\'es de LDAPlayer et vous disposez maintenant des outils essentiels pour exploiter un annuaire LDAP avec Pharo. Vous pouvez int\'eroger un annuaire, modifier ou effacer des entr\'ees selon vos besoins.

\paragraph{Liens utiles}
\url{http://www.openldap.org/},
\url{http://en.wikipedia.org/wiki/LDAP}
\url{http://fr.wikipedia.org/wiki/Lightweight_Directory_Access_Protocol}


%=========================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End: