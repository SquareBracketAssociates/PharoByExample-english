% $Author: Benjamin $
% $Date: 2011-12-21 14:28:33 +0200 $
% $Revision: 28563 $

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
    \newcommand{\cb}[1]{\nnbb{Camillo}{#1}} % Camillo Bruni
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {./figures/}}
	\begin{document}
\fi
%=================================================================
%\renewmessage{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================

\chapter{Spec}

Creating user interfaces programmatically is often a tedious and repetitive task. Some attempts to offer UI-Builders solved the problem on positioning UI elements and their inter connection. However, there is still the challenge of reusing widgets and the linked logic. There is a need for a declarative way to specify widgets allowing for composition and reuse of the UI declaration and logic. 

A Spec is a declarative way to describe a user interface. Used as a presenter, it is a major element of UI design and of applicative model reuse.

In this chapter, we will begin by an example of how to design a UI and most important, how to reuse an existing model and UI. Then the SpecInterpreter will be explained.

\section{Code}

The code is available on squeaksource:
\begin{code}{}
Gofer new
	url: 'http://ss3.gemstone.com/ss/Spec';
	package: 'ConfigurationOfSpec';
	load.
	
ConfigurationOfSpec loadFull.	
\end{code}

\subsection{Example}

The most important point of the spec is the reuse and the possibility of composition at two levels, UI and models. In this section we will show you how to build an abstract method browser (similar to the senders/implementor ) and how to reuse this browser to build a class and an extended method browser (similar to the code browser).

Here comes a simple example to quickly introduce to the use Specs. For now do not try to understand all the details but focus on the results. We will provide detailed insights right after the example.

\subsubsection{Methods Browser}

Firstly, let's think about how the visual structure of a method browser GUI. It is basically composed of two areas: a list and a text zone.Moreover there is an entry point which is the list of methods to be browsed.

So let's define a MethodBrowser class

\begin{classdef}{MethodBrowser class}
ComposableModel subclass: #MethodBrowser
	instanceVariableNames: 'listModel textModel'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Spec-Examples'
\end{classdef}

I let you write getters. The will be needed by the presenter.

It's still miss the entry point. By the way the entry point of the method browser could also be the list model's entry point.

\begin{method}{MethodBrowser>>\#methods:}
methods: aList

	"Here I reroute my entry point to the list model's entry point"
	self listModel items: aList
\end{method}

So now we have the entry point and the models. So lets make the connections between them.

\begin{method}{MethodBrowser>>\#initialize}
initialize
	"Initialization code for MethodBrowser"

	super initialize.

	listModel := self instantiate: ListComposableModel.
	textModel := self instantiate: TextModel.
		
	listModel whenSelectedItemChanges: [:selection |
		selection
			ifNil: [
				textModel text: ''.
				textModel behavior: nil ]
			ifNotNil: [:m | 
				textModel text: m sourceCode.
				textModel behavior: m methodClass ]].
	
	textModel aboutToStyle: true.
\end{method}

Now that links are created, we have to specify the spec on the class side:

\begin{method}{MethodBrowser class>>\#internSpec:}
internSpec

	^{ #ComposableSpec
			#add:. {{#model. #listModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. (0@0 corner: 1@0.5). nil}}.
			#add:. {{#model. #textModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. (0@0.5 corner: 1@1). nil}}}.
\end{method}
%
We specify the layout to be a column with submorphs added from top to bottom. And then for each model we add its spec recursively.

So if you evaluate
%
\begin{code}{}
| browser |
browser := MethodBrowser new.
browser openWithSpec.
\end{code}
You should have a window like the \figref{fig:methodBrowser1}.
\begin{figure}[ht]
\begin{center}
	\includegraphics[width=7cm]{MethodBrowser1}
	\caption{Our browser with an empty list}
	\figlabel{fig:methodBrowser1}
\end{center}
\end{figure}

And then, if you do:
%
\begin{code}{}
browser  methods: (ComposableModel methodDict values , ListComposableModel methodDict values).
\end{code}
The list should automatically be updated, and looks like \figref{fig:methodBrowser2}.
\begin{figure}[ht]
\begin{center}
	\includegraphics[width=7cm]{MethodBrowser2}
	\caption{Our browser with list of methods}
	\figlabel{fig:methodBrowser2}
\end{center}
\end{figure}

%\newpage
And if you select a method its source code should appear, like \figref{fig:methodBrowser3}

\begin{figure}[ht]
\begin{center}
	\includegraphics[width=7cm]{MethodBrowser3}
	\caption{Our browser with a method selected}
	\figlabel{fig:methodBrowser3}
\end{center}
\end{figure}

We are almost done, only the window title is still wrong.
So we will define
%
\begin{method}{MethodBrowser class>>\#title}
title

	^ 'Method Browser'
\end{method}

So now, if you re-evaluate the code, you should have a window like \figref{fig:methodBrowser4}

\begin{figure}[ht]
\begin{center}
	\includegraphics[width=7cm]{MethodBrowser4}
	\caption{Now with a nice title}
	\figlabel{fig:methodBrowser4}
\end{center}
\end{figure}

We can still improve the way the items are displayed. We just have to precise to the list model how to wrap items.
So let's add the following line into \verb+MethodBrowser>>#initialize+:
\begin{code}{}
listModel wrappingBlock: [:item | item methodClass name,'>>#', item selector ].
\end{code}

But you could also decide to add the items wrapping as a new entry point, so let's define 
\begin{method}{MethodBrowser>>\#wrapWith:}
wrapWith: aBlock

	listModel wrappingBlock: aBlock
\end{method}

We can now use this method into \verb+MethodBrowser>>#initialize+ to finally obtain
\begin{method}{MethodBrowser>>\#initialize}
initialize
	"Initialization code for MethodBrowser"

	super initialize.

	listModel := self instantiate: ListComposableModel.
	textModel := self instantiate: TextModel.
		
	listModel whenSelectedItemChanges: [:selection |
		selection
			ifNil: [
				textModel text: ''.
				textModel behavior: nil ]
			ifNotNil: [:m | 
				textModel text: m sourceCode.
				textModel behavior: m methodClass ]].
	
	textModel aboutToStyle: true.
	self wrapWith: [:item | item methodClass name,'>>#', item selector ].
\end{method}

And you finally get a browser which looks like \figref{fig:methodBrowser5}
\begin{figure}[ht]
\begin{center}
	\includegraphics[width=7cm]{MethodBrowser5}
	\caption{Voila}
	\figlabel{fig:methodBrowser5}
\end{center}
\end{figure}

\subsection{Classes and method browser}

Now, we want to build a class and method browser. To do that we need a list for class and a method browser. As previously the entry point is a list but this time a list of methods.

\begin{classdef}{ClassMethodBrowser class}
ComposableModel subclass: #ClassMethodBrowser
	instanceVariableNames: 'listModel methodModel'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Spec-Examples'
\end{classdef}

As previously, I let you implement the getters.

For the entry point, it's deja vu: 
\begin{method}{ClassMethodBrowser>>\#classes}
classes: aList

	self listHolder contents: aList
\end{method}

Now we have to create the link. It's simpler here than before because there is only one link to create between the list of classes and the method browser:

\begin{method}{ClassMethodBrowser>>\#initialize}
initialize
	"Initialization code for ClassMethodBrowser"

	super initialize.

	listModel := self instantiate: ListComposableModel.
	methodModel := self instantiate: MethodBrowser.
	
	listModel whenSelectedItemChanges: [:selection |
		selection 
			ifNotNil: [:class | 
				methodModel methods: (class methodDict values sort: [:a :b | a selector < b selector]).
				methodModel listModel resetSelection ]].
	
	methodModel wrapWith: [:method | method selector ].
\end{method}

So now, let's do the spec. 
\begin{method}{ClassMethodBrowser class>>\#internSpec}
internSpec2

	^ { #ComposableSpec.
			#add:. {{#model. #listModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. 	( 0@0 corner: 0.5@0.5 ). nil }}.
			#add:. {{#model. #methodModel.  #listModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. 	( 0.5@0 corner: 1@0.5 ). nil }}.
			#add:. {{#model. #methodModel.  #textModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. 	( 0@0.5 corner: 1@1 ). nil }}}.
\end{method}
In each spec we call the spec of our internal models.

Lastly, we can specify a title for the window:
\begin{method}{ClassMethodBrowser class>>\#title}
ClassMethodBrowser class>>#title

	^ 'Class Method Browser'
\end{method}

Then if you evaluate:

\begin{code}{}
| browser |
browser := ClassMethodBrowser new.
browser openWithSpec.
browser classes: (Smalltalk allClasses).
\end{code}

you will get a nice browser like in \figref{fig:methodBrowser6}.


\begin{figure}[ht]
\begin{center}
	\includegraphics[width=7cm]{MethodBrowser6}
	\caption{Our classes and method browser with a method selected}
	\figlabel{fig:methodBrowser6}
\end{center}
\end{figure}


Here ends this example. 
Basically you now know how to implement basic widget and how to compose Spec widgets together to get new powerful widgets.

Those examples (with few improvements) and few others are provided in the package \verb+Spec-Example+, feel free to have a look.

\section{Spec structure and Spec Interpretor}

Now that you've seen an example I will explain how it works and what is the principle of the mechanism behind.

\subsection{Spec Structure}\label{spec_structure}

The presenter side of a Spec is a structure used to simply hold a figurative representation of what you want your UI to look like. 

To have a static structure which can be easily inspected we have decided to use an array. It avoids having to implement our own parser, and can be analyzed regardless of the implementation of Smalltalk.

A spec is composed of a receiver followed by selector and arguments.
The first element of a spec can be either the name of a spec type (which knows what should be the receiver class), or a class name, or a symbol used as a binding which will return a class name. If the first element is a class name symbol, it will be turned into an instance of this class by the interpreter. A spec could be seen as a stack: each time a selector is read, as many arguments as needed by the selector are popped and analyzed (see the following example).

%stupid indentation is used to have a render almost aligned
\begin{method}{Example of a spec}
internSpec	<spec>		^ {#ListSpec.	
               #model:.                                        #model.
               #getListSizeSelector:.                   #listSize.
               #getIndexSelector:.                       #getIndex.
               #setIndexSelector:.                       #setIndex:.
               #getSelectionListSelector:.	         #getSelectionStateFor:.
               #setSelectionListSelector:.	         #setSelectionStateFor:at:.
               #getListElementSelector:. 	        #listElementAt:.
               #resetListSelector:.                       #resetListSelection.
               #getMenuSelector:.	                   #menu:shifted:.
               #keystrokeSelector:.                     #keyStroke:fromMorph:.
               #setMultipleSelection:.	               {#model. #multiSelection}.
               #wrapSelector:.                            #wrapItem:.
               #hResizing:.                                 #spaceFill.
               #vResizing:.                                 #spaceFill}
\end{method} 

As the specs are defined on class side, we have introduced a specific keyword \#model to create the binding between an instance and its spec.

\subsection{Spec Interpreter}

The goal of the Spec Interpreter is to build an instance starting from a spec.

The spec interpreter iterates over a spec and recursively builds each part of the spec. Right now, for instance creation, you can either specify a keyword (as \verb+#Panel+, \verb+#List+, \dots) bound to a rendering class using a specific dictionary or a class name. It allows the developer to separate the rendering part from the formal representation which is the presenter, and to simply change the binding to be able to create other interfaces with the same presenter (a dictionary for seaside objects for example).

\begin{method}{Example of a recursive spec}
internSpec

	^ { #ComposableSpec
			#add:. {{#model. #listModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. (0@0 corner: 1@0.5). nil}}.
			#add:. {{#model. #textModel. #internSpec.}.
						#layout:. {#FrameLayout.
									#fractions:offsets:. (0@0.5 corner: 1@1). nil}}}.
\end{method} 

\subsection{Spec types}

As mentioned in section \ref{spec_structure}, you can specify a type for your spec by referencing the a spec type name as the first keyword of your spec.

As a user of Spec, you should only used ComposableSpec as spec type since the other types are used to define a new set of basic widgets in order to bind Spec with a new widgets framework (as Morphic, Gaucho, Amber etc).

By using a spec type, you can defined default behavior shared among all the widgets, it means that you can also adapt all your widgets based on Spec by parametrizing only one object.

But the first keyword can not be resolved as a spec type name, the spec will be considered as a RawSpec which means that the spec is totally a stack. It will be kept this way during a while to ensure backward compatibility, but then maybe the default spec type should be ComposableSpec.
%to review

\section{Models and presenters construction}

\subsection{ComposableModel}

\verb+ComposableModel+ is an abstract class which is the superclass of all widget models used with specs.

There is a small API used to handle specs and the connections between the instances and their specs.

\begin{itemize}
	\item focusOrder: an ordered list of your sub-models sorted in the order the developer want the focus to change.
	\item internSpec: this is the default spec. The mechanism assumes each applicative model have such a method on class side.
	\item morph: an instance variable used to store the UI once the interpreter have created it.	
	\item title: this method return the title of the window used to render the presenter.
\end{itemize}

Then, each model has to define its own entry and exit points used to bind them together.

\subsection{Basic Models}

There is an widget model for each basic UI widget \ben{only image and window missing I guess} with their entry points for each customizable behavior of the widget and exit point for information you want to retrieve.
\ben{maybe should I introduce an abstract class to define basic widgets interfaces}

\paragraph{ListComposableModel} is the model lists.

The entry points are:
\begin{itemize}
	\item items: set the list of items to be displayed.
	\item menu: set the block used to indirect messages used to create menus.
	\item wrappingBlock: set the block used to wrap items from the list.
\end{itemize}
My exit point is:
\begin{itemize}
	\item selectedIndex: the index of the currently selected item (\verb+0+ if none).
	\item selectionItem: the currently selected item (\verb+nil+ if none).
\end{itemize}
You can communicate with this widget using:
\begin{itemize}
\item resetSelection: to reset the selection of the widget.
\item setSelectedIndex: : to manually set the index of the item you want to be selected.
\item setSelectedItem: : to manually set the item you want to be selected. 
\item updateList: to manually ask the list to refresh its own display.
\end{itemize}


\paragraph{TextModel} It is the model for text areas.

The entry points are:
\begin{itemize}
	\item actionToPerform: a value holder which contains a block performed when the modified text is saved.
	\item text: set the text to be displayed.
\end{itemize}
There are also entry points for text shouting:
\begin{itemize}	
	\item aboutToStyle: set wether or not the text should be shouted.
	\item aboutToStyleBlock: set the block used to know if wether or not the text should be shouted.
	\item behavior: set the behavior corresponding with the text currently displayed if relevant. It's used to properly colorize the syntax elements.
\end{itemize}	
You can communicate with this widget using:
\begin{itemize}	
	\item setSelection: : used to manually specify the range you want o be selected.
\end{itemize}

\paragraph{ButtonModel} It is the model for buttons.

The entry point is:
\begin{itemize}
	\item action: set the block executed when the button is clicked.
\end{itemize}	
There is no exit points.	

You can communicate with this widget using:
\begin{itemize}		
	\item label: : set the button's label.
	\item state: : set wether the button is on or off.
	\item enabled: : set wether the button is activated or not.
\end{itemize}

\paragraph{RadioButtonModel/CheckboxModel} They are the models for RadioButton and Checkbox. Since they are almost the same widget, they share the same API.

The entry points are:
\begin{itemize}
	\item activationAction: : set the action block to be valued when the item is activated.
	\item desactivationAction: : set the action block to be valued when the item is desactivated.
\end{itemize}

There is no exit points.	

You can communicate with this widget using:
\begin{itemize}		
	\item label: : set the button's label.
	\item state: : set wether the button is on or off.
	\item enabled: : set wether the button is activated or not.
\end{itemize}

\paragraph{DropListModel} It is the model for drop lists.

The entry points are:
\begin{itemize}
	\item items: set the list of items to be displayed. You have to provide a collection of DropListItem instances.
\end{itemize}
My exit point is:
\begin{itemize}
	\item selectedIndex: the index of the currently selected item (\verb+0+ if none).
	\item selectionItem: the currently selected item (\verb+nil+ if none).
\end{itemize}
You can communicate with this widget using:
\begin{itemize}
\item resetSelection: to reset the selection of the widget.
\item setSelectedIndex: : to manually set the index of the item you want to be selected.
\item setSelectedItem: : to manually set the item you want to be selected. 
\item updateList: to manually ask the list to refresh its own display.
\end{itemize}




\section{Conclusion}
As conclusion, you have now tools to quickly create UIs and to reuse them. Since the reuse is really a strong value for specs, keep in mind that your tools can be reused. So do not forget to provide a proper API for it.

%=========================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End: