
%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
%=================================================================
%\renewcommand{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================
\chapter{Mondrian}
\chalabel{mondrian}

\ja{we should verify the end of paragraphs: a lot of them have no end point}

Giving a meaning to a a large amount of data is challenging without adequate tools. Textual outputs are known to be limiting in their expression and interactions. 

Mondrian is an agile visualization engine. It is made to visualize and interact with any arbitrary data, defined in terms of objects and their relationships. Mondrian is commonly employed in software assessment activities. Mondrian excels at visualizing software source code. This chapter introduces Mondrian's principles and describes its expressive commands to quickly make up your data. After its reading, you will be able to create interactive and visual representation.

%=====================

\section{Installation and first visualization}

Mondrian is available via a Metacello configuration. Just open a workspace and type:

\begin{code}{}
Gofer new
	squeaksource: 'Mondrian'; 
	package: 'ConfigurationOfMondrian';
	load.
(Smalltalk at: #ConfigurationOfMondrian) loadDefault
\end{code}

There are essentially two ways to work with Mondrian. By either using the easel which provides an interactive scripting engine or by directly interacting with a view renderer. We will first use Mondrian in its easiest way, by using the easel. To open an easel, you can either use the World menu (it should contains the entry ``Mondrian Easel'') or execute the expression:

\begin{code}{}
MOEasel open.
\end{code}

In the easel you have just opened, you can see two panels: the one on top is the visualization panel, the second one is the script panel. In the script panel, enter the following code and press the \emph{generate} button:

\begin{code}{}
view nodes: (1 to: 20).
\end{code}
\begin{center}\includegraphics[scale=0.4]{picture1}\end{center}

You should see in the top pane 20 small boxes lined up in the top left corner. You have just rendered the numerical set between 1 and 20. Each box represents a number. The amount of interaction you can do is quite limited for now. You can only drag and drop a number and get a tooltip that indicates its value. We will soon see how to define interactions. For now, let us explore the basic drawing capabilities of Mondrian.

We can add edges between nodes that we already drawn. Add a second line:

\begin{code}{}
view nodes: (1 to: 20).
view edgesFrom: [ :v | v * 2 ].
\end{code}
\begin{center}\includegraphics[scale=0.4]{picture2}\end{center}

Each number is linked with its double. Not all the doubles are visualized. For example, the double of 20 is 400, which is not part of the visualization. In that case, no edge is drawn. 

The message \ct{edgesFrom:} defines one edge per node, when possible. For each node that has been added in the visualization, an edge is defined between this node and a node lookup from the provided block.

Mondrian contains a number of layouts to order nodes. We use the circle layout:

\begin{code}{}
view nodes: (1 to: 20).
view edgesFrom: [ :v | v * 2 ].
view circleLayout.
\end{code}

The visualization you obtain is:

\begin{center}\includegraphics[scale=0.4]{picture3}\end{center}

In the subsequent section we will visualize software code. Visualizing source code is often employed to discover patterns, useful when assessing code quality.

%=====================

\section{Visualizing the collection framework}

We will now visualize Smalltalk classes. In the remaining of this section, we will intensively use Smalltalk reflection to make compelling examples. Let's visualize the hierarchy of classes contained in the Collection framework:

\begin{code}{}
view nodes: Collection withAllSubclasses.
view edgesFrom: #superclass.
view treeLayout.
\end{code}
\begin{center}\includegraphics[scale=0.4]{picture4}\end{center}


We have used a tree layout to visualize Smalltalk class hierarchies. This layout is particularly adequate since Smalltalk is single-inheritance oriented. Collection is the root class of the Smalltalk collection framework library. The message \ct{withAllSubclasses} returns the list of its subclasses.

Classes are ordered vertically along their inheritance link. A superclass is above its subclasses. 

%=====================

\section{Reshaping nodes}

Mondrian visualizes graph of objects. Each object of the domain is associated to a graph element, a node or an edge. Graph element are not aware of their graphical representation. Graphical aspect is given by a shape. 

So far, we have solely use the default shape to represent node and edges. The default shape of a node is a five pixels wide square and the default shape of an edge is a thin straight gray line.

A number of dimensions defines the appearance of a shape: the width and the height of a rectangle, the size of a line dash, border and inner colors, for example. We will reshape the nodes of our visualization to convey more information about the internal structure of the classes we are visualizing. Consider:

\begin{code}{}
view shape rectangle
	width: [ :each | each instVarNames size * 3 ];
	height: #numberOfMethods.
view nodes: Collection withAllSubclasses.
view edgesFrom: #superclass.
view treeLayout.
\end{code}

\figref{picture5} shows the result. Each class is represented as a box. The \ct{Collection} class, the root of the hierarchy, is the top most box. The width of a class tells about the amount of instance variables it has. We multiply it by 3 for more contrasting results. The height tells about the number of methods. We can immediately spot classes with many more methods than others: \ct{Collection}, \ct{SequentiableCollection}, \ct{String}, \ct{CompiledMethod}. Classes with more variables than others are: \ct{RunArray} and \ct{SparseLargeTable}.

\begin{figure}[htbp]
\centerline{\includegraphics[width=\linewidth]{picture5}}
\caption{The system complexity for the collection class hierarchy.}
\label{fig:picture5}
\end{figure}

%=====================

\section{Multiple edges}

The message \ct{edgesFrom:} is used to draw one edge at most per node. A variant of it is \ct{edges:from:toAll:}. It support the definition of several edges starting from a given node. Consider the dependencies between classes. The script: 

\begin{code}{}
view shape rectangle size: [:cls | cls referencedClasses size ].
view nodes: ArrayedCollection withAllSubclasses.
view shape arrowedLine.
view 
	edges: ArrayedCollection withAllSubclasses from: #yourself toAll: #referencedClasses.
view circleLayout
\end{code}

The obtained visualization is given in \figref{classDependencies}.

\begin{figure}[htbp]
\centerline{\includegraphics[width=\linewidth]{classDependencies}}
\caption{Direct references between classes.}
\label{fig:classDependencies}
\end{figure}

\ct{String} and \ct{CompiledMethod} clearly shows up. These two classes contains many references to other classes.

We also see that \ct{text:} makes a shape contain a text.

Mondrian provides a whole bunch of utility methods to easily create elements.  Consider the expression:
\begin{code}{}
view edgesFrom: #superclass
\end{code}

\ct{edgesFrom:} is equivalent to \ct{edges:from:to:} :

\begin{code}{}
view edges: Collection withAllSubclasses from: #yourself to: #superclass.
\end{code}

itself equivalent to
\begin{code}{}
view 
  edges: Collection withAllSubclasses 
  from: [ :each | each yourself ] 
  to: [ :each | each superclass ].
\end{code}

%Multiple edges may be defined. In the previous section, edges where departed from the superclass to the running node. Alternatively, edges may depart from the running nodes to all subclasses. For that very purpose, we will use \ct{edges:from:toAll:}, a variant of \ct{edges:from:to:}.
%
%\begin{code}{}
%view shape rectangle
%  width: [ :each | each instVarNames size * 3];
%  height: [ :each | each methods size ].
%view nodes: Collection withAllSubclasses.
%view edges: Collection withAllSubclasses from: #yourself toAll: #subclasses.
%view treeLayout.
%\end{code}
%\begin{center}\includegraphics[scale=0.4]{picture6}\end{center}


%With \ct{edges:from:toAll:} and \ct{edgesFrom:}, two edges cannot start from a unique node.
%
%Time to time, you may need a finer grain for edge declaration \ja{can you explain what do you mean with finer grain ?}. The following is commonly employed as in:
%
%\begin{code}{}
%view nodes: (1 to: 5). 
%view shape arrowedLine. 
%view edges: { 1 -> 2 . 1 -> 5 . 4 -> 3 } from: #key to: #value. 
%view circleLayout
%\end{code}
%=====================

\section{Colored shapes}

A shape may be colored in various different way. Node shapes understand the message \ct{fillColor:}, \ct{textColor:}, \ct{borderColor:}. Line shapes understands \ct{color:}. Let's color the visualization of the collection hierarchy:

\begin{code}{}
view shape rectangle
	size: 10;
	borderColor: [ :cls | ('*Array*' match: cls name) 
										ifTrue: [ Color blue ] 
										ifFalse: [ Color black ] ];
	fillColor: [ :cls | cls isAbstractClass ifTrue: [ Color lightGray ] ifFalse: [ Color white] ].
view nodes: Collection withAllSubclasses.
view edgesFrom: #superclass.
view treeLayout.
\end{code}

The produced visualization is given in \figref{abstractClasses}. It easily help identifying abstract classes that are not named as ``Abstract'' and the one that are abstract without having an abstract method.

\begin{figure}[htbp]
\centerline{\includegraphics[width=\linewidth]{abstractClasses}}
\caption{Abstract classes are in gray and classes with the word ``Abstract'' in their name are in blue.}
\label{fig:abstractClasses}
\end{figure}

Similarly than with \ct{height:} and \ct{width:}, messages to defines color either takes a symbol, a block or a constant value as argument. The argument is evaluated against the domain object represented by the graphical element (the message \ct{moValue:} with the domain object is sent to the argument). 
The use of \ct{ifTrue:ifFalse:} is a bit cumbersome. Utilities methods are provided for that purpose to easily pick a color from a particular condition. The definition of the shape can simply be:

\begin{code}{}
view shape rectangle
	size: 10;
	if: [ :cls | ('*Array*' match: cls name) ] borderColor: Color blue;
	if: [ :cls | cls isAbstractClass ] fillColor: Color lightGray.
...
\end{code}

The method \ct{isAbstractClass} is defined on Behavior and Metaclass. By sending the \ct{isAbstractClass} to a class return a boolean value telling us whether the class is abstract or not. We recall that an abstract class in Smalltalk is a class that defines or inherits at least one  abstract method (i.e., which contain \ct{self subclassResponsibility}).


%=====================

\section{More on colors}

Colors are pretty useful to designate a property (\eg gray if the class is abstract). They may also be employed to represent a continuous distribution. For example, the color intensity may indicate the result of a metric. Consider the previous script in which the node color intensity tells about the number of lines of Code:

\begin{code}{}
view interaction action: #browse.
view shape rectangle
	width: [ :each | each instVarNames size * 3];
	height: [ :each | each methods size ];
	linearFillColor: #numberOfLinesOfCode within: Collection withAllSubclasses.
view nodes: Collection withAllSubclasses.
view edgesFrom: #superclass.
view treeLayout.
\end{code}

\figref{systemComplexity} shows the resulting picture. The message \ct{linearFillColor:within:} takes as first argument a block function that return a numerical value. The second argument is a group of elements that is used to scale the intensity of each node. The block function is applied to each element of the group. The greatest value is set to black. The fading scales from 0 to this maximum. The maximum intensity is given by the maximum \ct{#numberOfLinesOfCode} can take for all the subclasses of \ct{Collection}. 

The visualization\footnote{This visualization is named 'System complexity', if you wish to know more about this visualization, you can refer to 'Polymetric Views---A Lightweight Visual Approach to Reverse Engineering' (Transactions on Software Engineering, 2003).} you now obtain put in relation for each class the number of methods, the number of instance variables and the number of lines of code. Differences in size between classes might suggest some maintenance activities. 

Variants of \ct{linearFillColor:within:} are \ct{linearXXXFillColor:within:}, where \ct{XXX} is one among \ct{Blue}, \ct{Green}, \ct{Red}, \ct{Yellow}.

\begin{figure}[htbp]
\centerline{\includegraphics[width=6cm]{systemComplexity}}
\caption{The system complexity visualization: nodes are classes; height is the number of lines of methods; width the number of variables; color tells about the number of lines of code.}
\label{fig:systemComplexity}
\end{figure}

A color may be assigned to an object identity using \ct{identityFillColorOf:}. The argument is either a block or a symbol, evaluated against the domain object. A color is associate to the result of the argument.

%=====================

\section{Popup view}

Let's jump back on the abstract class example. The following script indicates abstract classes and how many abstract methods they define:

\begin{code}{}
view shape rectangle	size: [ :cls | (cls methods select:  #isAbstract ) size * 5 ] ;	if: #isAbstractClass fillColor: Color lightRed;	if: [:cls | cls methods anySatisfy: #isAbstract ] fillColor: Color red.view nodes: Collection withAllSubclasses.view edgesFrom: #superclass.view treeLayout.\end{code}

\begin{figure}[htbp]
\centerline{\includegraphics[width=0.6\linewidth]{abstractClasses2.png}}
\caption{Boxes are classes and links are inheritance relationships. The amount of abstract method is indicated by the size of the class. A red class defines abstract methods and a pink class inherits from an abstract class solely.}
\label{fig:abstractClasses}
\end{figure}

\figref{abstractClasses} indicate classes that are abstract either by inheritance or by defining abstract methods. Class size indicates the amount of abstract method defined. 

The popup message can be enhanced to list abstract methods. Putting the mouse above a class does not only give its name, but also the list of abstract methods defined in the class. The following piece of code has to be added at the beginning:

\begin{code}{}
view interaction popupText: [ :aClass | 
  | stream |
  stream := WriteStream on: String new.
  (aClass methods select: #isAbstract thenCollect: #selector)
    do: [:sel | stream nextPutAll: sel; nextPut: $ ; cr].
  aClass name printString, ' => ', stream contents ].
...
\end{code}

So far, we have seen that a graphical element has a shape to describe its graphical representation. It also contains an \emph{interaction} that contains event handlers. The message \ct{popupText:} takes a block as argument. This block is evaluated with the domain object as argument. The block has to return the popup text content. In our case, it is simply a list of the methods.

In addition to a textual content, Mondrian allows a view to be pop up. We will enhance the previous example to illustrate this point. When the mouse enters a node, a new view is defined and displayed next to the node. The method \ct{popupView:} receives a two-arguments block as parameters. 

\begin{code}{}
view interaction popupView: [ :element :secondView | 	secondView node: element forIt: [	  secondView shape rectangle 	    if: #isAbstract fillColor: Color red;	    size: 10.  	  secondView nodes: (element methods sortedAs: #isAbstract).	  secondView gridLayout gapSize: 2 	] ].view shape rectangle	size: [ :cls | (cls methods select:  #isAbstract ) size * 5 ] ;	if: #isAbstractClass fillColor: Color lightRed;	if: [:cls | cls methods anySatisfy: #isAbstract ] fillColor: Color red.view nodes: Collection withAllSubclasses.view edgesFrom: #superclass.view treeLayout.\end{code}

The argument of \ct{popupView:} is a two argument block. The first parameter of the block is the element represented by the node located below the mouse. The second argument is a new view that will be opened.

In the example, we used \ct{sortedAs:} to order the nodes representing methods. This method is defined on Collection and belongs to Mondrian. To see example usages of \ct{sortedAs:}, browse its corresponding unit test:
\begin{code}{}
ToolSet browse: MOViewRendererTest selector: #testSortedAs 
\end{code}

This last example use the message \ct{node:forIt:} in the popup view to define a subview.

%=====================

\section{Subviews}

A node is a view in itself. This allows for a graph to be embedded in any node. The embedded view is physically bounded by the encapsulating node. The embedding is realized via the keywords \ct{nodes:forEach:} and \ct{node:forIt:}. 

The following example approximates the dependencies between methods by linking methods that may call each other. A method \ct{m1} is connected to a method \ct{m2} if \ct{m1} contains a reference to the selector \ct{#m2}. This is a simple but effective way to see the dependencies between methods. Consider:

\begin{code}{}
view nodes: Collection withAllSubclasses forEach: [:cls |	view nodes: cls methods.	view edges: cls methods from: #yourself toAll: [ :cm | cls methods select: [ :rcm |  cm messages anySatisfy: [:s | rcm selector == s ] ] ].	view treeLayout].view interaction action: #browse.view edgesFrom: #superclass.view treeLayout.
\end{code}

A subview contains its own layout. Interactions and shapes defined in a subview are not accessible from a nesting node. 

%view shape rectangle
%  if: #isAbstractClass fillColor: Color lightRed.
%view nodes: Collection withAllSubclasses forEach: [ :aClass | 
%  view shape rectangle 
%    if: #isAbstract fillColor: Color red.
%  view nodes: (aClass methods sortedAs: #isAbstract).
%  view gridLayout gapSize: 2. 
%].
%view edgesFrom: #superclass.
%view treeLayout.


\begin{figure}[htbp]
\centerline{\includegraphics[width=0.6\linewidth]{methodDependencies.png}}
\caption{Large boxes are classes. Inner boxes are methods. Edges shows a possible invocation between the two.}
\label{fig:abstractClasses}
\end{figure}



%Abstract classes are painted in light red and abstract method in intense red. 
%Each node may embed any arbitrary view. Consider this script to visualize class hierarchies for each package of the Collection framework.
%
%\begin{code}{}
%packages := PackageOrganizer default packageNames
%        select: [ :name | name beginsWith: 'Collections-' ] 
%        thenCollect:  [ :name | PackageInfo named: name ].
%        
%view nodes: packages forEach: [ :aPackage | 
%  view nodes: (aPackage classes reject: #isTrait).
%  view edgesFrom: #superclass.
%  view treeLayout
%].
%\end{code}
%
%We see an interesting issue here. A little more information is here shown. Not only we see the hierarchies defined in each packages, but we also see inter-package links. We can suppress relations between packages and indicate in blue classes \ja{we never integrated blue classes, what is it ?} for which their superclasses lives in a different package.
%
%\begin{code}{}
%packages := PackageOrganizer default packageNames
%        select: [ :name | name beginsWith: 'Collections-' ] 
%        thenCollect:  [ :name | PackageInfo named: name ].
%        
%view nodes: packages forEach: [ :aPackage | 
%  | classes |
%  classes := aPackage classes reject: #isTrait.
%  view shape rectangle 
%    if: [ :cls | (classes includes: cls superclass) not ] fillColor: Color blue.
%  view nodes: classes.
%  classes := classes copy select: [ :cls | classes includes: cls superclass ].
%  view edges: classes from: #superclass to: #yourself.
%  view treeLayout
%].
%\end{code}
%
%Package names can be included above each box \ja{only the concerned line :)}.
%
%\begin{code}{}
%packages := PackageOrganizer default packageNames
%        select: [ :name | name beginsWith: 'Collections-' ] 
%        thenCollect:  [ :name | PackageInfo named: name ].
%
%view shape rectangle withoutBorder.
%view nodes: packages forEach: [ :aPackage | 
%  view shape label text: #packageName.
%  view node: aPackage.
%  view node: aPackage forIt: [
%    | classes |
%    classes := aPackage classes reject: #isTrait.
%    view shape rectangle 
%      if: [ :cls | (classes includes: cls superclass) not ] fillColor: Color blue.
%    view nodes: classes.
%    classes := classes copy select: [ :cls | classes includes: cls superclass ].
%    view edges: classes from: #superclass to: #yourself.
%    view treeLayout
%  ].
%  view verticalLineLayout center
%].
%\end{code}
%=====================

\section{Forwarding events}

Method of the visualization given in the previous section may be moved by a simple drag and drop. However, it may be wished that the methods have a fixed position, and only the classes can be drag-and-dropped. In that case, the message \ct{forward} has to be sent to the interaction. Consider:

\begin{code}{}
view nodes: MOShape withAllSubclasses forEach: [:cls |	view interaction forward.	view shape rectangle 					size: #numberOfLinesOfCode.	view nodes: cls methods.	view edges: cls methods from: #yourself toAll: [ :cm | cls methods select: [ :rcm |  cm messages anySatisfy: [:s | rcm selector == s ] ] ].	view treeLayout].view interaction action: #browse.view edgesFrom: #superclass.view treeLayout.
\end{code}

Moving a method will move the class instead. As most operating systems, Mondrian offers multiple selection using the Ctrl or Cmd key. This default behavior is available for every nodes.

%=====================

\section{Events}

Each mouse movement, click and keyboard keystroke corresponds to a particular events. Mondrian offers a rich hierarchy of events. The root of the hierarchy is MOEvent. To associate a particular action to an event, an handler has to be defined on the object interaction. On the following example, double clicking on a class opens a code browser:

\begin{code}{}
view shape rectangle  width: [ :each | each instVarNames size * 5 ];  height: [ :each | each methods size ];  if: #isAbstractClass fillColor: Color lightRed;  if: [:cls | cls methods anySatisfy: #isAbstract ] fillColor: Color red.  view interaction on: MOMouseDouble do: [ :ann |   ann modelElement browse].view nodes: Collection withAllSubclasses.view edgesFrom: #superclass.view treeLayout.\end{code}

The block handler accepts one argument: the event generated. The object that triggered the event is obtained by sending \ct{modelElement} to the event object. 

%=====================

\section{Interaction}
Mondrian offers a number of contextual interaction mechanisms. The interaction object contains a number of keywords for that purpose. The message \ct{highlightWhenOver:} takes a block as argument. This block returns a list of the node to highlight when the mouse enters a node. Consider the example:

\begin{code}{}
view interaction 
  highlightWhenOver: [:v | {v - 1 . v + 1. v + 4 . v - 4}].
view shape rectangle 
  width: 40;
  height: 30;
  withText.
view nodes: (1 to: 16).
view gridLayout gapSize: 2.
\end{code}

Entering the node 5 highlights the nodes 4, 6, 1 and 9. This mechanism is quite efficient to not overload with connecting edges. Only the information is shown for the node of interest. 

A more compelling application of \ct{highlightWhenOver:} is with the following example. A hierarchy of class is displayed on the left hand side. On the right hand size a hierarchy of unit tests is displayed. Locating the mouse pointer above a unit test highlights the classes that are referenced by one of the unit test method. Consider the (rather long) script:

\begin{code}{}
"System complexity of the collection classes"
view shape rectangle
  width: [ :each | each instVarNames size * 5 ];
  height: [ :each | each methods size ];
  linearFillColor: #numberOfLinesOfCode within: Collection withAllSubclasses.
view nodes: Collection withAllSubclasses.
view edgesFrom: #superclass.
view treeLayout.

"Unit tests of the package CollectionsTest"
view shape rectangle withoutBorder.
view node: 'compound' forIt: [
  view shape label.
  view node: 'Collection tests'.
  
  view node: 'Collection tests' forIt: [
    | testClasses |
    testClasses := (PackageInfo named: 'CollectionsTests') classes reject: #isTrait.
    view shape rectangle
      width: [ :cls | (cls methods inject: 0 into: [ :sumLiterals :mtd | sumLiterals + mtd allLiterals size]) / 100 ];
      height: [ :cls | cls numberOfLinesOfCode / 50 ].
    view interaction 
        highlightWhenOver: [ :cls | ((cls methods inject: #() 
                        into: [:sum :el | sum , el allLiterals ]) select: [:v | v isKindOf: Association ] thenCollect: #value) asSet ].
    view nodes: testClasses.
    view edgesFrom: #superclass.
    view treeLayout ].
  
  view verticalLineLayout alignLeft
].
\end{code}

\begin{figure}[htbp]
\centerline{\includegraphics[width=\linewidth]{testCoverage.png}}
\caption{Interactive system complexity.}
\label{fig:interactiveTestCoverage}
\end{figure}


The script contains two parts. The first part is the ubiquitous system complexity of the collection framework \ja{give us the number of lines}.  The second part renders the tests contained in the CollectionsTests \ja{explain more about this part, maybe line by line}. The width of a class is the number of literals contained in it. The height is the number of lines of code. Since the collection tests makes a great use of traits to reuse code, these metrics have to be scaled down. When the mouse is put over a test unit, then all the classes of the collection framework referenced in this class are highlighted. 

%=====================

\section{Conclusion}

Mondrian enables any graph of objects to be visualized. This chapter has reviewed the principal features of Mondrian:
\begin{itemize}
\item The most common way to defines nodes is wit \ct{nodes:} and edges with \ct{edgesFrom:}, \ct{edges:from:to:} and \ct{edges:from:toAll:}.
\item A whole range of layout are offered. The most common layouts are accessible by sending \ct{circleLayout}, \ct{treeLayout}, \ct{gridLayout} to a view.
\item A shape defines the graphical aspect of an element. Height and width are commonly set with \ct{height:} and \ct{width:}, respectively. 
\item A shape is colored with \ct{borderColor:} and \ct{fillColor:}.
\item Information my be popped up with \ct{popupText:} and \ct{popupView:}.
\item A subview is defined with \ct{nodes:forEach:} and \ct{node:forIt:}.
\item Events of a sub node is forward to its parent with \ct{forward} and \ct{forward:}.
\item Highlighting is available with \ct{highlightWhenOver:}, which takes a one-arg block that has to returns the list of node to highlight.
\end{itemize}

%Mondrian is a perpetual evolving application. This chapter intents to cover the features frequently used. If there is a topic you wish to see discussed here, send an email to alexandre@bergel.eu.
%
%We hope you haved enjoyed it!
%
%=====================


%=============================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi
%=============================================================


%Dead text:

Node color \ja{nothing about edge color ? I think we can say that it is possible too} is an important information support, as the width and the height of a node. Color should be easy to pick to represent particular condition.

The keyword if:fillColor: enables one to assign a color for a particular condition. Consider we want to extend the previous example by coloring abstract classes in red.

\begin{code}{}
view shape rectangle
  width: [ :each | each instVarNames size * 3 ];
  height: [ :each | each methods size ];
  if: #isAbstractClass fillColor: Color red.
view nodes: Collection withAllSubclasses.

view edges: Collection withAllSubclasses from: #yourself toAll: #subclasses.

view treeLayout.
\end{code}


The message \ct{if:fillColor:} may be sent to a shape to conditionally set a color. \ja{what is the link with the source code below ?}

\begin{code}{}
view shape rectangle
  width: [ :each | each instVarNames size * 3];
  height: [ :each | each methods size ];
  if: #isAbstractClass fillColor: Color red.
view nodes: Collection withAllSubclasses.

view edgesFrom: #superclass.

view treeLayout.
\end{code}

All red nodes represent abstract class. Waving the moose above a node make a text tooltip appear revealing its name. 

\ja{it should not be here, we are in section 'adding colors'}
Extended possibilities exist to define interaction. We will review them in a future section. For now, if you are interested in opening a system browser directly from a node, you define this interaction: \ja{can you display only the line concerned ? it is difficult to read each time the same code with only one line changed}

\begin{code}{}
view interaction action: #browse.
view shape rectangle
  width: [ :each | each instVarNames size * 3];
  height: [ :each | each methods size ];
  if: #isAbstractClass fillColor: Color red.
view nodes: Collection withAllSubclasses.

view edgesFrom: #superclass.

view treeLayout.
\end{code}

You can easily \ja{I think there are too much 'easy' or 'easily' in the chapter} spot some red node that do not have subclasses. This indicates a design flow since an abstract must to have subclasses. It makes no sense for a class that is not supposed to be instantiated (since it is abstract) to not have a subclass.

The very same analyzes can be realized on your own classes.

\ja{is this code useful ? it seems to be the same as above}

\begin{code}{}
view interaction action: #browse.
view shape rectangle
  width: [ :each | each instVarNames size * 3];
  height: [ :each | each methods size ];
  if: #isAbstractClass fillColor: Color red.
view nodes: (PackageInfo named: 'Mondrian') classes.

view edgesFrom: #superclass.

view treeLayout.
\end{code}

A shape may contains more than one condition. Let's distinguish abstract classes from classes that define abstract methods.

\begin{code}{}
view shape rectangle
  width: [ :each | each instVarNames size * 3];
  height: [ :each | each methods size ];
  if: #isAbstractClass fillColor: Color lightRed;
  if: [:cls | cls methods anySatisfy: #isAbstract ] fillColor: Color red.

view nodes: Collection withAllSubclasses.

view edgesFrom: #superclass.

view treeLayout.
\end{code}

All red nodes are still abstract classes. Light red indicates classes that do not define abstract methods; strong red indicates classes that define at least one abstract method.

%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End: