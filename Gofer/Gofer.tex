% $Author: jannik $
% $Date: 2010-05-13 23:11:04 +0200 (Thu, 13 May 2010) $
% $Revision: 32942 $

% HISTORY:
% 2010-05-14 - Jannik started chapter

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\pagestyle{headings}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
	% \renewcommand{\nnbb}[2]{} % Disable editorial comments
	\sloppy
\fi
%=================================================================

%=================================================================


\chapter{Gofer: Scripting package loading}
\chalabel{gofer}

Pharo proposes powerful tools to manage source code such as semantics-based merging, tree diff-merge, and a git-like distributed versioning system. In particular as presented in the Monticello Chapter, Pharo uses a package system named Monticello. In this chapter after a first reminder of the key aspects of Monticello we will show how we can script package using Gofer. Gofer is a simple API for Monticello. It is used by Metacello, the language to manage package maps that we present in Chapter Metacello. 



\section{Preamble: Package management system}

\paragraph{Packages.}
A package is a list of class and method definition. In Pharo a package is not associated with a namespace. A package can extend a class defined in another package: it means that a package, for example \ct{Network} can add methods to the class \ct{String}, even though \ct{String} is not defined in the package \ct{Network}. Class extensions support the definition of layers and allows for the natural definition of packages. 


\begin{figure}
\begin{center}\includegraphics[width=6cm]{classExtension}\end{center}
\end{figure}

To define a package, you simply need to declare one using the Monticello browser and to define a class extensions, it is enough to define a method with a category starting with `*' followed by the package name (here \ct{'*network'}). 

\begin{code}{}
Object subclass: #ButtonsBar
    instanceVariableNames: ''
    classVariableNames: ''
    poolDictionaries: ''
    category: 'Zork'
\end{code}    


\begin{figure}
\begin{center}
\includegraphics[width=9cm]{browserExtension}
\caption{The browser shows that the class \ct{String} gets the methods \ct{asUrl} and \ct{asUrlRelativeTo:} from the package \ct{network-url}}
\end{center}
\end{figure}

We can get the list of changes of a package before publication by simply selecting the package and clicking on the \button{Changes} of the Monticello Browser. 


\begin{figure}
\begin{center}
\includegraphics[width=9cm]{changeBrowser}
\caption{The change browser shows that the method \ct{String>>asUrl} has changed.}
\end{center}
\end{figure}

\paragraph{Package Versioning System.}
A version management system helps for version storage and keeps an history of system evolution. Moreover, it provides the management of concurrent accesses to a source code repository. It keeps traces of all saved changes and allows us to collaborate with other engineers. More a project grows, more it is important to use a version management system.

Monticello defines the package system and version management of Pharo. In Pharo, classes and methods are elementary entities which are versioned by Monticello when actions were done (superclass change, instance variable changes, methods adding, changing, deleting \ldots). A source is an HTTP server which allows us to save projects (particularly packages) managed by Monticello. This is the equivalent of a forge: It provides the management of contributors and their status, visibility information, a wiki with RSS feed. A source open to everybody is available at \url{http://www.squeaksource.com/}.


\paragraph{Distributed architecture of Monticello.} Monticello is a distributed version control management system like git but dedicated to Smalltalk. Monticello manipulates source code entities such as classes, methods,... It is then possible to manage local and distributed code servers. Gofer allows one to script such servers to publish, download and synchronize servers. 


Monticello uses a local cache for packages. Each time a package is required, it is first looked up in this local cache. In a similar way, when a package is saved, it is also saved in the local cache.  From a physical point of a view a Monticello package is a zipped file containing meta-data and the complete source code of package. 
To be clear in the following we make the distinction between a package loaded in the pharo image and a package saved in the cache but not loaded. A package currently loaded is called a working copy of the package. We also define the following terms: image (object and bytecode executed by the virtual machine), loaded package (downloaded package from a server that is loaded in memory), dirty package (a loaded package with unsaved modifications). A dirty package is a loaded package.


For example, in Figure~\ref{fig:archi} the package \ct{a.1} is loaded from the server squeaksource. It is not modified. The package \ct{b.1} is loaded from the server yoursource.com but it is modified locally in the image.
Once \ct{b.1} which was dirty is saved on the server yoursource.com, it is versioned into \ct{b.2} which is saved in the cache and the remote server. 


\begin{figure}
\begin{center}
\includegraphics[width=9cm]{Architecture3}
\caption{(left)Typical setup with clean and dirty packages loaded and cached  --- (right) Package published. \label{fig:archi}}
\end{center}
\end{figure}


%\begin{figure}
%%\begin{center}
%\includegraphics[width=7cm]{Architecture2}
%\end{center}
%\end{figure}






\section{What is Gofer?}
Gofer is a scripting tool for Monticello. It is developed by Lukas Renggli and it is used by Metacello (the map and project management system built on top of Monticello). Gofer supports the easy creation of scripts to load, save, merge, update, fetch ... packages. In addition, Gofer makes sure that operations let the system in a clean state. Gofer allows one to load packages located in different repositories in a single operation, to load the latest stable version or the currently developed version. Gofer is part of basis of Pharo since Pharo 1.0.
Metacello uses Gofer as its underlying infrastructure to load complex projects. Gofer is more adapted to load simple package. 

You can ask Gofer to update it itself by executing the following expression: 

\begin{code}{}
Gofer gofer update
\end{code}

\section{Using Gofer}
Using Gofer is simple: you need to specify a location, the package to be loaded and finally the operation to be performed. The location often represents a file system been it an HTTP, a FTP or simply an hard-disc.   The location is the same as the one used to access a Monticello repository.For example it is  \ct{'http://www.squeaksource.com/MyPackage'} as used in the following expression. 

\begin{code}{A}
MCHttpRepository 
    location: 'http://www.squeaksource.com/MyPackage'
    user: 'pharoUser' 
    password: 'pharoPwd'
\end{code}

Here is a typical Gofer script: it says that we want to load the package \ct{PBE2GoferExample} from the repository \ct{PBE2GoferExample} that is available on \ct{http://www.squeaksource.com}.

\begin{code}{}
Gofer new
    url: 'http://www.squeaksource.com/PBE2GoferExample';
    package:'PBE2GoferExample';
    load
\end{code}

When the repository (HTTP or FTP) requires an identification, the message \ct{url:username:password:} is available.  The message \ct{directory:} supports the access to local files. 

\begin{code}{}
Gofer new
    "we work on the project PBE2GoferExample"
    url: 'http://www.squeaksource.com/PBE2GoferExample'	
    "we specify a user name"
    username: 'pharoUser'
    "we specify a password" 
    password: 'pharoPwd'; 
    "we define the package to be loaded"
    package:'PBE2GoferExample'; 
    "we disable the lookup of package in the local cache"
    disablePackageCache; 
    "we stop the error raising"
    disableRepositoryErrors; 
    "we load the specified package"
    load.
\end{code}


Since we often use the same public servers, there are some shortcuts in the API to use them. For example, \ct{squeaksource:} is a shortcut for \ct{http://www.squeaksource.com}, \ct{wiresong:} for \ct{http://source.wiresong.ca/} and \ct{gemsource:} for \ct{http://seaside.gemstone.com/ss/}.
In such a case you do not need to specify the full path as shown with the next snippet:

\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample'; 
    load
\end{code}







In addition, when Gofer does not succeed to load a package in a specified URL, it looks in the local cache which is normally at the root of your image. It is possible to force Gofer not to use the cache using the message \ct{disablePackageCache} or to use it using the message \ct{enablePackageCache}.

In a similar manner, Gofer returns an error when one of the repositories is not reachable. We can instruct it to ignore such errors using the message \ct{disableRepositoryErrors}. To enable it the message we can use the message \ct{enableRepositoryErrors}.




\subsection{Package Identification}
Once an URL and the option are specified, we should define the packages we want to load. Using the message \ct{version:} defines the exact version to load, while the message \ct{package:} should be used to load the latest version available in all the repositories.

The following example load the version 2 of the package. 
\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    version: 'PBE2GoferExample-janniklaval.1';
    load
\end{code}

We can also specify some constraints to identify packages using the message \ct{package: aString constraint: aBlock} to pass a block. 

For example the following code will load the latest version of the package saved by the developer named \ct{janniklaval}.

\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample' constraint: [ :version | version author = 'janniklaval' ];
    load
\end{code}



\section{Gofer Actions}

%\subsection{Installing a package}
%Comme nous l'avons évoqué dans les paragraphes précédents, on peut utiliser Gofer pour charger une version spécifique d'un package, charger la dernière version sauvegardée sur le serveur distant, ou encore  charger la dernière version sauvegardé par un programmeur particulier:
%
%\begin{code}{}
%Gofer new
%    "we will load the latest version of the configuration of Seaside "
%    squeaksource: 'MetacelloRepository';
%    package: 'ConfigurationOfSeaside30'; 
%    load	
%
%"Now to load seaside you need to ask its configuration.
%(Smalltalk at: #ConfigurationOfSeaside30) load.
%\end{code}
%
%Pay attention that the last expression will load the complete seaside application, \ie around 70 packages. 

\subsection{Loading several packages}
We can load several packages from different servers. To show you a concrete example, you have to load first the configuration of Seaside using its Metacello configuration.

\begin{code}{}
Gofer new
    "we will load the latest version of the configuration of Seaside "
    squeaksource: 'MetacelloRepository';
    package: 'ConfigurationOfSeaside30'; 
    load.

"Now to load seaside you need to ask its configuration."
(Smalltalk at: #ConfigurationOfSeaside30) load.
\end{code}

Pay attention that the last expression will load the complete seaside application, \ie around 70 packages, so it can take a moment.

The following code snippet loads multiple packages from different servers.  The loading order is respected: the script loads first \ct{Pier-All}, then \ct{Picasa-Model}, and finally  \ct{Picasa-Seaside}.

\begin{code}{}
Gofer new 		
    renggli: 'pier';
    package: 'Pier-All';
    squeaksource: 'PicasaClient';
    package: 'Picasa-Model';
    package: 'Picasa-Seaside';
    load.
\end{code}

This example may give the impression that \ct{Pier-All} is looked up in the Pier repository of the server \ct{renggli} and that \ct{Picasa-Model} and \ct{Picasa-Seaside} are looked up in the project \ct{PicasaClient} of the squeaksource server. However this is not the case, Gofer does not take into account this order. In absence of version number, Gofer loads the most recent package versions found looking in the two servers.

We can then rewrite the script in the following way:

\begin{code}{}
Gofer new 
    squeaksource: 'PicasaClient';		
    renggli: 'pier';
    package: 'Pier-All';
    package: 'Picasa-Model';
    package: 'Picasa-Seaside';
    load.
\end{code}

When we want to specify that package should be loaded from a specific server, we should write multiple scripts. 

\begin{code}{}
Gofer new
    squeaksource: 'PicasaClient';
    package: 'Picasa-Model';
    package: 'Picasa-Seaside';
    load.
Gofer new
    squeaksource: 'PicasaClientLightbox';
    package: 'Picasa-Model';
    package: 'Picasa-Seaside';
    load.
\end{code}    

Note that such scripts load the latest versions of the packages, therefore they are fragile since if a new package version is published, you will load it even if this is inappropriate. 
In general it is a good practice to control the version of the external components we rely on and use the latest version for our own current development. Now such problem can be solved with Metacello which is the tool to express configurations and load them. 

\subsection{Other protocols}

Gofer supports also FTP as well as loading from a local directory. We basically use the same messages than before with some changes. 

For FTP, we should specify the URL using \ct{'ftp'} as heading 

\begin{code}{}
Gofer new
    url: 'ftp://wtf-is-ftp.com/code';
    ...
\end{code}

To work on a local directory, the message \ct{directory:} followed by the absolute path of the directory should be used. Here we specify that the directory to use is reachable at \ct{/home/pharoer/hacking/MCPackages}

\begin{code}{}
Gofer new
    directory: '/home/pharoer/hacking/MCPackages';
\end{code}

Finally it is possible to look for packages in a repository and all its subfolders using the keen star. 

\begin{code}{}
Gofer new
    directory: '/home/pharoer/hacking/MCPackages/*';
    ...
    \end{code}

Once a Gofer instance is parametrized, we can send it messages to perform different actions. 
Here is a list of the possible actions. Some of them are described later. 

\begin{tabular}{lp{8cm}}
\hline
\textsf{load}&Load the specified packages.\\
\textsf{update}&Update the package loaded versions.\\
\textsf{merge}&Merge the distant version with the one currently loaded.\\
\textsf{localChanges}&Show the list of changes between  the bases version and the version currently modified.\\
\textsf{remoteChanges} & Show the changes between the version currently modified and the version published on a server.\\
\textsf{cleanup}&Cleanup packages: System obsolete information is cleaned.\\
\textsf{commit / commit:}&Save the packages on a distant server -- with a message log.\\
\textsf{revert}&Reload previously loaded packages.\\
\textsf{recompile}&recompile packages\\
\textsf{unload}&Unload from the image the packages\\
\textsf{fetch}&Download the remote package versions from a remote server to the local cache.\\
\textsf{push}&Upload the versions from the local cache to the remote server.\\
\end{tabular}


\subsection{Working with remote servers}
Since Monticello is a distributed versioning control system, it is often useful to synchronize  versions published on a remote server with the ones locally published in the MC local cache. Here we show the main operations to support such tasks.

\paragraph{The \ct{merge}, \ct{update} and \ct{revert} operations.}
The message \ct{merge} performs a merge between a remote version and the working copy (the one currently loaded). 
Changes present in the working copy are merged with the code of the remote one.
It is often the case that after a merge, the working copy gets dirty and should be republished. The new version will contain the current changes and the changes of the remote version. In case of conflicts the user will be warned, else the operation will happen silently. 

\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample';
    merge
\end{code}
    
The message \ct{update} loads the remote version in the image. The modifications 
of the working copy are lost. 

The message \ct{revert} resets the local version, \ie it loads again the current version. The changes of the working copy are then lost. 
 
\paragraph{The \ct{commit} and \ct{commit:} operations.}
Once we have merged or changed a package we want to save it. For this we can use
the messages \ct{commit} and \ct{commit:}. The second one is expecting a comment - this is in general a good practice. 

\begin{code}{}
Gofer new
    "We save the package in the repository"
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample';
    "We comments the changes and save"
    commit: 'I try to use the message commit: '
\end{code}    


\paragraph{The \ct{localChanges} and \ct{remoteChanges} operations.}

Before loading or saving a version, it is often useful to verify the changes 
made locally or on the server. The message \ct{localChanges}  shows the changes between the last loaded version and the working copy. 
The \ct{remoteChanges} shows the differences between the working copy and the last published version on the server. 
Both return a list of changes. 

\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample'; 
    "We check that we will publish only our changes by comparing local changes versus the packages published on the server"
    localChanges
\end{code}

Using the messages \ct{browseLocalChanges} and \ct{browseRemoteChanges}, it is possible to browse the changes using a normal code browser.

\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    "on ajoute la dernière version de PBE2GoferExample"
    package: 'PBE2GoferExample';
    "on navigue dans les changements effectués sur le serveur"
    browseRemoteChanges		
\end{code}    
%/// Image : screenshot.png ///

\paragraph{The \ct{unload} operation.}
The message \ct{unload} unloads from the image the packages. Note that using the Monticello browser you can delete a package but such operation does not remove the code of the classes associated with the package, it just destroys the package. Unloading a package destroys the packages and the classes it contains. 

The following code unloads the packages and its classes from the current image.
\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample';
    unload
\end{code}    

Note that you cannot unload Gofer itself that way. \ct{Gofer gofer unload} does not work. 


\paragraph{The \ct{fetch} and \ct{push} operations.} Since Monticello is a distributed versioning system, it is good to save locally all the versions you want, without being forced to published on a remote server - this is especially true when working off-line. Now this is tedious to synchronize all the local and remote published packages. The messages \ct{fetch} and \ct{push} are there to support you in this task.

The message \ct{fetch} copies from the remote server the packages that are missing in your local server. The packages are not loaded in Pharo. After a fetch you can load the packages even if the remote server breaks down. 


\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample';
    fetch
\end{code}    

Now if you want load your packages locally remember to set up that the lookup should consider local cache and disable errors as presented in the beginning of this chapter  (messages  \ct{disableRepositoryErrors} and \ct{enablePackageCache}).

The message \ct{push} performs the inverse operation. It published to the remote server the packages locally available. All the packages that you published locally are then pushed to the server. 

\begin{code}{}
Gofer new
    squeaksource: 'PBE2GoferExample';
    package: 'PBE2GoferExample';
    push
\end{code}    

As a pattern, we always keep in our local cache the copies of all the versions of our projects or the projects we used. This way we are autonomous from any network failure and the packages are backed up in our regular backup.

With these two messages, it is easy to write a script \ct{sync} that synchronize local and remote repositories. 
\begin{code}{}
Gofer new
     squeaksource: 'PBE2GoferExample';
     package: 'PBE2GoferExample';
     push.
Gofer new
     squeaksource: 'PBE2GoferExample';
     package: 'PBE2GoferExample';
     fetch
\end{code}


\subsection{Automating Answers}
Sometimes package installation asks for information such as passwords. With the systematic use of a build server, packages will probably stop to do that, but this is important to know how to supply answers from within a script to these questions.
The message \ct{valueSupplyingAnswers:} supports such a task. 


\begin{code}{}
[ Gofer new
	squeaksource: 'Seaside30';
	package: 'LoadOrderTests';
	load ]
	valueSupplyingAnswers: {
		{'Load Seaside'. True}.
		{'SqueakSource User Name'. 'pharoUser'}.
		{'SqueakSource Password'. 'pharoPwd'}.
		{'Run tests'. false}.
	}
\end{code}

This message should be sent to a block giving a list of questions and their answers as shown by the previous examples


\section{Some useful scripts}
Here is a script to fetch all the packages of a given project. This is useful to grab all your files and get a version locally.

\begin{code}{}
| go |
go := Gofer new.
go squeaksource: 'Pharo14'.
go allResolved
	do: [:each |
			self crLog: each packageName.
			go 
				squeaksource: 'Pharo14';
				package: each packageName;
				fetch]
\end{code}


The following script publishes files from your local cache to a given repository.

\begin{code}{}
| go |
go := Gofer new.
go repository: (MCHttpRepository
	location: 'http://ss3.gemstone.com/ss/Pharo14';
	user: 'pharoUser'
	password: 'pharoPwd').

((FileDirectory on:
	(FileDirectory default fullNameFor: 'package-cache'))
	fileAndDirectoryNames
		select: [:each | '*.mcz' match: each])
					do: [:f | go version: ('.' join: (f findTokens: $.) allButLast); push]
\end{code}


\section{Conclusion}
Gofer provides a robust and stable implementation to script the
management of your packages. Now when you project grows you should
really consider to use Metacello (see \charef{metacello}).

%=================================================================

\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

